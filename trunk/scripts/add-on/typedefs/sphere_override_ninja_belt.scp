//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED: Friday, April 1, 2011
VERSION=0.56b


[COMMENT typedef_t_ninja_belt]
This script should be improved further:
- There should be a possibility of missing... currently you never miss
- There should be a throwing delay... currently you can cause a LOT of
  damage VERY quickly using a simple macro
- The hard coded english should be replaced with cliloc
- The belt should use tooltips to display the number of shurikens in the belt
- The belt should use context menus to load and unload shurikens... currently
  there is no way to unload them
- The belt should be a container so that it (and the player) gains weight when
  shurikens are loaded
- There should be a limit to how many shurikens can be loaded into the belt...
  currently it is infinite (although if the container/weight concept is added
  that is likely sufficient)
- The range of potential damage should probably come from the shuriken itself
  as opposed to being hard-coded here

[TYPEDEF t_ninja_belt]
ON=@DClick
IF (<TAG0.SHURIKEN>)
   IF (<SRC.OSTR> >= 10)
      TARGET=Throw a shuriken at what creature?
   ELSE
      SRC.SYSMESSAGE=You are not strong enough to use this.
   ENDIF
ELSE
   SRC.SYSMESSAGE=You are out of shurikens.
ENDIF
RETURN 1

ON=@TargOn_Char
IF (<SRC.TARG.CANSEELOS>)
   IF (<SRC.TARG.DISTANCE> < 7)
      IF (<TAG0.SHURIKEN>)
         TAG0.SHURIKEN -= 1
         SRC.EFFECT=0,i_shuriken,4,4,0
         SRC.DAMAGE=<EVAL {10,12}> 01 <SRC.UID>
         SRC.SOUND=snd_combat_shuriken_hit
      ELSE
         SRC.SYSMESSAGE=You are out of shurikens.
      ENDIF
   ELSE
      SRC.SYSMESSAGE=You are too far away.
   ENDIF
ELSE
   SRC.SYSMESSAGE=You can not see that.
ENDIF
RETURN 1

ON=@DropOn_Self
IF (<ARGO.BASEID> == i_shuriken)
   TAG0.Shuriken += <ARGO.AMOUNT>
   ARGO.REMOVE
   RETURN 1
ENDIF

[eof]
