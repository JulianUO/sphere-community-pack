//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED: 
VERSION=0.56b
//
// SCRIPT CREDITS
// --------------
// Coruja - Base script
// ShiryuX - Updated to the pack
//
// SCRIPT DOCUMENTATION
// --------------------
// Functions used for Blacksmithing Menu

[function f_craft_blacksmith_checkforge]
if (!<isneartype t_forge 3>) || (!<isneartype t_anvil 3>)
return 0
endif
ctag.craft.message = "1044267"
return 1

[function f_craft_blacksmith_exceptionalbonus]
name = Exceptional <name>
if <isweapon>
tag0.damageincrease += 35
elif <isarmor>
modar += <eval (<armor.high> * 15) / 100>
endif

[function f_craft_blacksmith_repair]
targetf f_craft_blacksmith_repair_targ
sysmessage <def.scp.craft_repair_targ>

[function f_craft_blacksmith_repair_targ]
if (!<isneartype t_forge 3>) || (!<isneartype t_anvil 3>)
sysmessage <def.scp.craft_blacks_forgeanvil>
return 1
elif !<argo.isitem>
sysmessage <def.scp.craft_repair_notitem>
return 1
elif <argo.topobj> != <uid>
sysmessage <def.scp.craft_repair_inpack>
return 1
elif (<argo.skillmake.1.key> != skill_blacksmith) || !((<argo.type> == t_armor) || (<argo.type> == t_shield) || (<argo.isweapon>))
sysmessage <def.scp.craft_repair_wrongskill>
return 1
elif <argo.more1l> >= <argo.more1h>
sysmessage <def.scp.craft_repair_fullrepair>
return 1
endif
if <argo.more1h> <= 0
sysmessage <def.scp.craft_repair_cannot>
return 1
endif
f_loopanim 11,3,1
f_loopsound 42,3,1
local.difficulty = <eval ((((<argo.more1h> - <argo.more1l>) * 1250) / <argo.more1h>) - 250) / 10>
local.decrease = <eval 30 - (<blacksmithing> / 33)>
if <local.decrease> < 0
local.decrease = 0
endif
local.decrease += <eval {2 4}>
argo.more1h = <eval (<argo.more1h> * (100 - <local.decrease>)) / 100>
if <local.difficulty> > <r100>
sysmessage <def.scp.craft_repair_fail>
return 1
else
argo.more1l = <argo.more1h>
sysmessage <def.scp.craft_repair_success>
endif

[function f_craft_blacksmith_smelt]
targetf f_craft_blacksmith_smelt_targ
sysmessage <def.scp.craft_smelt_targ>

[function f_craft_blacksmith_smelt_targ]
if (!<isneartype t_forge 3>) || (!<isneartype t_anvil 3>)
sysmessage <def.scp.craft_blacks_forgeanvil>
return 1
elif !<argo.isitem>
sysmessage <def.scp.craft_smelt_notitem>
return 1
elif (<argo.type>==t_clothing) || (<argo.attr>&attr_magic) || !((<argo.isarmor>) || (<argo.isweapon>) || strmatch("*ingot*","argo.resources.1>"))
sysmessage <def.scp.craft_smelt_notsmeltable>
return 1
else
	if !<argo.quality>
	local.amount = 1
	else
	local.amount = <muldiv <strarg <argo.resources.1>>,60,100>
	endif
serv.newitem <streat <argo.resources.1>>,<dlocal.amount>
new.cont = <argo.cont>
argo.remove
sysmessage <def.scp.craft_smelt_success>
endif

[eof]