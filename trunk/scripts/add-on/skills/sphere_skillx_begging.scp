[function f_begging_start]
if <argo.isitem>
return 1
endif
if <argo.brain>
	if (<argo.body> == c_man) || (<argo.body> == c_woman)
		if <distance <argo.uid>> < 4
			if <findlayer.25.uid>
			sysmessage @,,2 500404 // They seem unwilling to give you any money.
			else
				if <tag0.begged.<argo.uid>>
				sysmessage @,,2 500404 // They seem unwilling to give you any money.
				else
				face <argo.uid>
				f_loopanim 32,4,1
				argo.say @,,2 500405 // I feel sorry for thee...
					if <skillsuccess <r1000>,begging>
					timerf 4,f_begging_success <argo.uid>
					else
					timerf 4,sysmessage @,,2 500404 // They seem unwilling to give you any money.
					endif
				endif
			return 0
			endif
		else
		sysmessage @,,2 <argo.sex 500401/500402> // You are too far away to beg from him/her.
		endif
	else
	sysmessage @,,2 500399 // There is little chance of getting money from that!
	endif
else
sysmessage @,,2 500398 // Perhaps just asking would work better.
endif
return 1

[function f_begging_success]
ref79 = <args>
if <ref79.gold> < 10
say @,,2 500407 // I have not enough money to give thee any!
else
local.getgold = <eval <ref79.gold>/10>
local.max = <eval 50+(<fame>/250)>
	if <local.max> > 100
	local.max = 100
	elif <local.max> < 50
	local.max = 50
	endif
	if <local.getgold> > <local.max>
	local.getgold = <local.max>
	endif
ref79.consume <local.getgold> i_gold
serv.newitem i_gold,<local.getgold>,<uid>
ref79.say @,,3 1008002,0,<dlocal.getgold> gold.
tag0.begged.<args> = 1
timerf 300,tag0.begged.<args>
endif
return 1

[eof]