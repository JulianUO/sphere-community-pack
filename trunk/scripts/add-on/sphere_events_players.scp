//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED: 06-Apr-2009
VERSION=0.56b
//
// SCRIPT DOCUMENTATION
// --------------------
// [EVENTS] @userstats - OSI STATS + BONUS
// - Max Hits: (Str / 2) + Str Bonus + 50 + Max Hits Bonus
// - Max Mana: Int + Int Bonus + Max Mana Bonus
// - Max Stam: Dex + Dex Bonus + Max Stam Bonus

// [EVENTS] @spellcast (TO-DO: PUT FASTER CASTING (ARGN3) IN HERE)
// - Words of Power for new spells (necro, chivalry, etc)
// - Plate armor restriction and Weapon unequip
// - Integration of Mage Armor and Mage Weapon
// - Spell Cast Recovery and addition of Faster Cast Recovery

// [EVENTS] @spelleffect
// - Checks for Harm Spell (Spell Damage Bonus)
// - Checks for Magic Reflection Spell
// - Implements Magic Resistance Skill

// [EVENTS] @gethit - SPELL DAMAGE BONUS
// - Eval. Int. Bonus: ((EvalInt * 3) / 100) + 1
// - Intelligence Bonus: (INT / 10) (Drop decimals from result)
// - GM Inscription Bonus: 10% Damage Bonus
// - Spell Damage Increase: Int. Bonus + Inscr. Bonus + Spell Damage Increase Items*
// - Total Damage: (Base Damage * Eval. Int. Bonus) * ((Spell Damage Increase / 100) + 1) (Drop decimals from result) 
// - Mind Blast damage: Base Damage + ((Magery + Intelligence) / 5) (Drop decimals from result)
// * Spell Damage Increase Items effects are capped at 15% in Player vs. Player combat.

// SCRIPTPACK ENCHANTS
// -------------------
// *DONE* Damage Increase
// Defense Chance Increase (Depends on Combat System)
// *DONE* Dexterity Bonus
// *DONE* Faster Cast Recovery
// Faster Casting (Not even started)
// Hit Chance Increase (Depends on Combat System)
// *DONE* Hitpoints Increase
// *DONE* Intelligence Bonus
// *DONE* Lower Mana Cost
// *DONE* Lower Reagent Cost
// Luck (There's no real effect on this one)
// *DONE* Mage Armor
// *DONE* Mage Weapon
// *DONE* Mana Increase
// Slayer Weapon (Uncomplete, need to add different types of slayers, etc)
// *DONE* Spell Damage Increase
// *DONE* Strength Bonus

[events e_PlayerGenericEvent]
// New @SkillPreSelect Trigger
on=@userextcmd
if <argn1> == 36
	if <trigger @SkillPreSelect,<def.tat_as_argn>,<argv[0]>> == 1
	return 1
	endif
endif

on=@skillpreselect
// Skill recovery snippet
if <tag0.skillrecovery> > <serv.time>
src.sysmessage <def.scp.skill_mustwait>
return 1
endif

if <argn1> == 21
	if <def.scp.NewSkills_Hiding>
	f_start_hiding
	return 1
	endif
elseif <argn1> == 47
	if <def.scp.NewSkills_Stealth>
	f_start_stealth
	return 1
	endif
endif

// Context Menu Display
on=@contextmenurequest
if <uid> == <src.uid>
	if <guild>
	src.addcontextentry 101,6117
	else
	src.addcontextentry 101,6117,01
	endif
src.addcontextentry 102,6119
else
src.addcontextentry 200,6121
endif
return 0

// Context Menu Functions
on=@contextmenuselect
if <uid> == <src.uid>
	if <argn> == 101
	groster
	elif <argn> == 102
	openquestlog
	endif
else
	if <argn> == 200
	src.face <uid>
	endif
endif

// Quest Button
on=@userquestbutton
openquestlog

// OSI Guildstones Menu
on=@userguildbutton
src.GuildMenu
return 1

// Cancel use of Book of Arms
// Until we script it and finish it!
on=@userspecialmove
return 1

// OSI Calculation + Stats Bonus
on=@userstats
if <def.scp.Followers> == 1
maxfollower = <def.scp.MaxFollowers>
endif
if <def.scp.OSIStats> == 1
maxhits = <eval (<str>/2) + <modstr> + 50 + <tag0.modmaxhits>> 
maxmana = <eval <int> + <modint> + <tag0.modmaxmana>>
maxstam = <eval <dex> + <moddex> + <tag0.modmaxstam>>
endif

// Followers control
on=@itembuy
if <def.scp.followers> == 1
	if <act.type> == t_figurine
	local.slots = <qval <serv.chardef.<act.tdata3>.tag0.control.slots> ? <serv.chardef.<act.tdata3>.tag0.control.slots> : 1>
		if <eval <src.curfollower>+(<local.slots>*<argn>)> > <src.maxfollower>
		src.sysmessage @,,2 1049612,<serv.chardef.<act.tdata3>.name> // ~1_NAME~ remained in the stables because you have too many followers.	
		src.newgold <local.totalcost>
		return 1
		endif
	src.curfollower += <eval <local.slots>*<argn>>
	endif
endif

// Crafting System
on=@skillmenu
doswitch <def.scp.CraftingSystem>
return 0
f_crafting_startosi <args>
enddo
return 1

on=@spellselect
// Tithing is needed even if it is not used at all.
// So this needs to be check before going into LRC and LMC.
if (<argn1> > 199) && (<argn1> < 211)
	if <tithing> < <def.scp.spell.<argn1>.tithing>
	sysmessage <def.scp.magery_notithing>
	return 1
	endif
endif
if <def.scp.MagicProperties_LowerReagentCost> == 1
	if <argn3> == 2
		if <argn1> < 200
		f_lrc_check <argn1>,<argn2>,<argn3>
		elif <argn1> > 600
		return 6
		else
		f_lrc_check_chival <argn1>,<argn2>,<argn3>
		endif
	return 6
	endif
else
	if <def.scp.MagicProperties_LowerManaCost> == 1
	argn2 = <eval (<argn2> * (<tag0.LowerManaCost> + 1)) / 100>
	endif
endif

// Necromancy Words of Power
// Armor Restriction + Mage Armor + Mage Weapon
// Faster Cast Recovery
on=@spellcast
if <dtag0.spellrecovery> > <serv.time>
src.sysmessage @03c1,6,2 502644 // You have not yet recovered from casting a spell
return 1
endif
if <def.scp.OSISpellDiff> == 1
local.skill = <serv.skill.<streat <serv.spell.<argn1>.skillreq>>.key>
actdiff = <eval (((<<local.skill>> - <argn2>) * 2.5) + 50.0)>
endif
if !<npc>
	if <isempty <serv.spell.<argn1>.runes>>
		if <account.plevel> > 2
			if <serv.wopstaff>
				if <isnum <def.scp.spell_<argn1>_wop>>
				say @<serv.wopcolor>,<serv.wopfont>,2 <def.scp.spell_<argn1>_wop>
				else
				say @<serv.wopcolor>,<serv.wopfont> <def.scp.spell_<argn1>_wop>
				endif
			endif
		elif <account.plevel> < 2
			if <isnum <def.spell.<argn1>.wop>>
			say @<serv.wopcolor>,<serv.wopfont>,2 <def.scp.spell_<argn1>_wop>
			else
			say @<serv.wopcolor>,<serv.wopfont> <def.scp.spell_<argn1>_wop>
			endif
		endif
	endif
endif
if <def.scp.MagicProperties_MageWeapon> == 1
	if <argn1> != 104 // exception for Curse Weapon
		if <findlayer.1.uid>
			if !(<findlayer.1.istevent.t_enchant_mage_weapon>) || (<findlayer.1.type> == t_wand)
			findlayer.1.unequip
			endif
		endif
	endif
endif
if <findlayer.2.uid>
	if <findlayer.2.isweapon>
		if <argn1> != 104 // exception for Curse Weapon
			if <def.scp.MagicProperties_MageWeapon> == 1
				if !(<findlayer.2.istevent.t_enchant_mage_weapon>)
				findlayer.2.unequip
				endif
			endif
		endif
	else
		if <def.scp.MagicProperties_MageArmor> == 1
			if !(<findlayer.2.istevent.t_enchant_mage_armor>)
			findlayer.2.unequip
			endif
		endif
	endif
endif
if <def.scp.MagicProperties_MageArmor> == 1
	for 1 <def.armor_amount>
	ref5 = <findlayer.<def.armor_part_<dlocal._for>>.uid>
		if <ref5.type> == t_armor
			if !(<ref5.istevent.t_enchant_mage_armor>)
			src.sysmessage You can't cast with this armor (<ref5.name>).
			return 1
			endif
		endif
	endfor
endif

// OSI Spells Bonus
// Magic Resistance Skill + Magic Reflection Spell
on=@spelleffect
if <def.scp.MagicProperties_FasterCastRecovery> == 1
src.tag0.spellrecovery = <eval <serv.time>+(12-(<min <eval <src.tag0.fastercastrecovery>*2>,12>))>
endif
if <isdamagespell <argn1>>
tag0.gothitbyspell = <argn1>
endif
if <flags>&statf_reflection
	if <src.uid> != <uid>
		if (<serv.spell.<argn1>.flags>&spellflag_harm) || <argn> == 41
		flags &= ~statf_reflection
		findid.i_rune_magic_reflection.remove
		effect 3,i_fx_bless_effect,16,16
			if <src.flags>&statf_reflection
			src.flags &= ~statf_reflection
			src.findid.i_rune_magic_reflection.remove
			src.effect 3,i_fx_bless_effect,16,16
			else
			src.spelleffect <argn1>,<argn2>
			endif
		return 1
		endif
	endif
endif
if <def.scp.NewSkills_ResistingSpells> == 0
return 2
endif
if <src.uid> == <uid>
argn3 = 0
else
	if <serv.spell.<argn1>.flags>&spellflag_resist
	local.resist = <eval <magicresistance>-((<src.evaluatingintel>*<spellcircle <argn1>>)/20)>
		if <local.resist> > (<r>)
		sysmessage @,,2 501783 // You feel yourself resisting magical energy.
		argn3 -= <r<eval <magicresistance>/6>,<eval <magicresistance>/4>>
		else
		argn3 -= <r<eval <magicresistance>/7>>
		endif
	endif
endif
return 2

// Slayer Weapons + New Parrying 
// Damage Increase
on=@hit
if <def.scp.MagicProperties_SlayerWeapon> == 1
	if <src.brain>
		if <argo.istevent.t_enchant_slayer_weapon>
			if (strmatch(*<src.baseid>*,<def.<argo.tag0.slayergroup>_slayer>))
			argn1 *= 2
			src.effect 3,i_fx_glow,16,15,15
			endif
		endif
	endif
endif
if <def.scp.MagicProperties_DamageIncrease> == 1
	if <tag0.damageincrease>
	local.damageincrease = <qval (<tag0.damageincrease> >= <def.enchant.1.limit>) ? <def.enchant.1.limit> : <tag0.damageincrease>>
	argn1 += <eval ((<local.damageincrease>*<argn1>)/100)+1>
	endif
endif
if (<src.findlayer.1>) || (<src.findlayer.2>)
	if <src.f_cs_chancetoblock> > <r>
		if (<src.findlayer.2.type> == t_shield) || (<src.findlayer.2.isweapon>)
			if (<src.trysrc <uid> findlayer.2.trigger @ParryTest>)
			return 0
			endif
		elif <src.findlayer.1.isweapon>
			if (<src.trysrc <uid> findlayer.1.trigger @ParryTest>)
			return 0
			endif
		endif
	src.sysmessage "You parry the blow."
	sysmessage "<src.name> parries the blow."
	src.sound 0146
	src.anim 30
	return 1
	endif
endif

// OSI Spells Bonus
on=@gethit
if <def.scp.OSISpellDamage> == 0
	if <def.scp.MagicProperties_SpellDamageIncrease> == 1
	argn1 += <qval (<src.tag.spelldamageincrease> > 15) ? 15 : <src.tag.spelldamageincrease>>
	return 0
	endif
return 0
endif
if <tag0.gothitbyspell>
local.damage = <r<serv.spell.<dtag.gothitbyspell>.effect>>
local.evalint = <eval (((<src.evaluatingintel>*3)/1000)+1)>
local.damageinc = <eval <src.int>/10>
	if <src.inscription> >= 100.0
	local.damageinc += 10
	endif
	if <def.scp.MagicProperties_SpellDamageIncrease> == 1
		if <src.tag0.spelldamageincrease>
		local.damageinc += <qval (<src.tag.spelldamageincrease> > 15) ? 15 : <src.tag.spelldamageincrease>>
		endif
	endif
argn1 = <eval <local.damage>*<local.evalint>>
argn1 += <eval ((<argn1>*<local.damageinc>)/100)+1>
tag.gothitbyspell
endif

// Proper Statlock Fix
on=@statchange
local.statsum = <qval <tag0.override.statsum> ? <tag0.override.statsum> : <serv.skillclass.0.statsum>>
local.stats = <eval <ostr>+<odex>+<oint>>
if <local.stats> == <local.statsum>
	if <statlock[<argn1>]> == lock_locked
	return 1
	endif
	if <statlock[<argn1>]> == lock_up
		for 0 2
			if <local._for> != <argn1>
				if <statlock[<dlocal._for>]> == lock_down
					if (<eval <o<def.stat_<dlocal._for>_name>>-1> > 0)
					o<def.stat_<dlocal._for>_name> = <eval <o<def.<stat_<dlocal._for>_name>>>-1>
					return 0
					endif
				return 1
				endif
			endif
		endfor
	return 1
	endif
endif

// EA Karma Fame
on=@famechange
if <def.scp.EAKarmaFame> == 1
gainfame 
famelevelcheck
settitle
return 0
endif

// EA Karma Fame
on=@karmachange
if <def.scp.EAKarmaFame> == 1
gainkarma
karmalevelcheck
settitle
return 0
endif

// EA Karma Fame
// Quest System
on=@kill
if <def.scp.EAKarmaFame> == 1
ctag.fame.kill = <argo.fame>
ctag.karma.kill = <argo.karma>
endif
if !(<argo.isplayer>)
	if <tag0.quests>
		for <tag0.quests>
			for x 1 <def.quest_<dtag0.quest<dlocal._for>.id>_objectiveamount>
				if <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_type> == 2
					if (<argo.baseid> == <streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>)
						if (<tag0.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>)
							if !(<isempty <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_location>>)
								if (strmatch(*<def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_location>*,<argo.region.name>))
								try tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>> = <hval <tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>> - 1>
								endif
							else
							try tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>> = <hval <tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>> - 1>
							endif
							if !(<tag0.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>)
							sysmessage @,,2 1075050
							else
							sysmessage @,,2 1075051,<dtag0.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>
							endif
						endif
					endif
				endif
			endfor
		endfor
	endif
endif

[eof]