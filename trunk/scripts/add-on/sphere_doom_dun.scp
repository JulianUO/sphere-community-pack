// ****************************************************************************
// SPHERE by : Menasoft 1997-2005
// www.sphereserver.com
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
// ****************************************************************************
// FILE LAST UPDATED: Monday, Sept 26, 2005
// by ClouD_BR
VERSION=0.56b

[AREADEF a_doom]
NAME=Doom
P=2366,1268,-85,3
Rect=256,2,512,557,3
FLAGS=region_flag_nobuilding|region_antimagic_recall_in|region_antimagic_gate|region_antimagic_recall_out|region_flag_underground|region_antimagic_teleport
GROUP=Malas

[AREADEF a_doom_gauntlet]
NAME=Doom Gauntlet
P=428,341,-1,3
Rect=260,322,515,558,3
FLAGS=region_flag_nobuilding|region_antimagic_recall_in|region_antimagic_gate|region_antimagic_recall_out|region_flag_underground|region_antimagic_teleport
GROUP=Malas

[PLEVEL 4]
GENERATEDOOM

[FUNCTION GENERATEDOOM]
IF !(<VAR0.GENERATEDDOOM>)
SERV.NEWITEM 067d
NEW.P=355,15,-1,3
NEW.TYPE=T_DOOR_LOCKED
NEW.TIMER
NEW.ATTR=ATTR_MOVE_NEVER
VAR.DARK_GUARDIAN_DOOR1=<NEW>
NEW.TIMERF 1,UID.<NEW>.TIMER -1
SERV.NEWITEM 067f
NEW.P=355,14,-1,3
NEW.TYPE=T_DOOR_LOCKED
NEW.TIMER
NEW.ATTR=ATTR_MOVE_NEVER
VAR.DARK_GUARDIAN_DOOR2=<NEW>
NEW.TIMERF 1,UID.<NEW>.TIMER -1
SERV.NEWITEM 0fea
NEW.COLOR=03bc
NEW.P=365,15,-1,3
NEW.ATTR=ATTR_MOVE_NEVER
SERV.NEWITEM i_chest_metal
NEW.P=364,10,-2,3
NEW.COLOR=04ec
NEW.ATTR=ATTR_MOVE_NEVER
SERV.NEWITEM i_ankh
NEW.ATTR=ATTR_MOVE_NEVER
NEW.P=491,52,-1,3
SERV.NEWITEM 03
NEW.ATTR=ATTR_MOVE_NEVER
NEW.P=491,51,-1,3
SERV.NEWNPC c_sylvana_sorceress
NEW.P=422,198,-1,3
SERV.NEWITEM=i_bell_doom
NEW.ATTR=ATTR_MOVE_NEVER
NEW.P=417,246,22,3
SERV.NEWITEM i_floor_tile_grey
NEW.COLOR=0ad
NEW.ATTR=ATTR_MOVE_NEVER
NEW.P=324,64,-1,3
LOCAL.1=<R1,5>
LOCAL.2=<R1,5>
WHILE (<LOCAL.2> == <LOCAL.1>)
LOCAL.2=<R1,5>
ENDWHILE
LOCAL.3=<R1,5>
WHILE ((<LOCAL.3> == <LOCAL.1>) || (<LOCAL.3> == <LOCAL.2>))
LOCAL.3=<R1,5>
ENDWHILE
LOCAL.4=<R1,5>
WHILE ((<LOCAL.4> == <LOCAL.1>) || (<LOCAL.4> == <LOCAL.2>) || (<LOCAL.4> == <LOCAL.3>))
LOCAL.4=<R1,5>
ENDWHILE
NEW.TAG.CORRETO <EVAL <LOCAL.1>>.<EVAL <LOCAL.2>>.<EVAL <LOCAL.3>>.<EVAL <LOCAL.4>>
LOCAL.CHAO=<NEW>
FOR 4
SERV.NEWITEM i_lever
NEW.ATTR=ATTR_MOVE_NEVER
NEW.COLOR=36
NEW.TYPE=T_LAMP_LEVER
NEW.LINK=<LOCAL.CHAO>
DOSWITCH <EVAL <LOCAL._FOR>-1>
BEGIN
NEW.P=323,71,2,3
NEW.MOREP=323,72,-1,3
END
BEGIN
NEW.P=316,64,2,3
NEW.MOREP=316,65,-1,3
END
BEGIN
NEW.P=323,58,2,3
NEW.MOREP=324,58,-1,3
END
BEGIN
NEW.P=332,63,2,3
NEW.MOREP=332,64,-1,3
END
ENDDO
TRY UID.<LOCAL.CHAO>.TAG.DOOM.LEVER<EVAL <LOCAL._FOR>>=<NEW>
ENDFOR
SERV.NEWITEM i_box_brass
NEW.TYPE=T_CAIXA_DOOMLAMP
NEW.P=469,96,5,3
SERV.NEWITEM 053b
NEW.TYPE=T_CHAO_SAIRLAMPROOM
NEW.COLOR=025
NEW.P=469,92,-1,3
SERV.NEWITEM i_doom_fancy_wind_chimes
NEW.P=461,99,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_inquisitor_resolution
NEW.P=471,99,-1,3
NEW.ATTR=030
SERV.NEWITEM i_doom_lamppost
NEW.P=472,96,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_skull_candle
NEW.P=287,61,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_Tower_Lantern
NEW.P=349,32,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_lamppost
NEW.P=439,10,0,3
NEW.ATTR=010
SERV.NEWITEM i_blade_righteous
NEW.P=494,59,-3,3
NEW.ATTR=010
SERV.NEWITEM i_doom_painting_ruined
NEW.P=290,104,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_stretched_hide
NEW.P=267,171,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_guillotine
NEW.P=314,144,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_gruesomestandard
NEW.P=359,187,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_skull_candle
NEW.P=426,172,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_cards_tarot
NEW.P=394,156,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_goat_skinned
NEW.P=493,173,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_brazier
NEW.P=295,247,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_giftbox
NEW.P=362,220,0,3
NEW.ATTR=010
SERV.NEWITEM i_doom_painting_ruined
NEW.P=328,216,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_goat_skinned
NEW.P=348,233,0,3
NEW.ATTR=010
SERV.NEWITEM i_doom_stretched_hide
NEW.P=370,234,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_lamppost
NEW.P=432,196,-1,3
NEW.ATTR=010
SERV.NEWITEM i_doom_titan_hammer
NEW.P=494,212,-1,3
NEW.ATTR=010

SERV.NEWITEM i_multi_pentagram
NEW.P=423,430,-1,3
NEW.MULTICREATE

////First lesser boss/////

SERV.NEWITEM i_doom_gauntlet_floor
NEW.P=470,432,-1,3
NEW.TAG.GOP=481,432,-1,3
NEW.TAG.LESSERBOSS=C_DARKNIGHT_CREEPER
NEW.TAG.LESSERBOSS.P=492,448,-1,3
NEW.TAG.PENTAGRAMP=427,422,-1,3
REF1=<NEW> //first boss floor
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=474,432,-1,3
NEW.TAG.LESSERBOSS=C_DARKNIGHT_CREEPER
NEW.MOREP=467,432,-1,3
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=474,433,-1,3
NEW.TAG.LESSERBOSS=C_DARKNIGHT_CREEPER
NEW.MOREP=467,432,-1,3
SERV.NEWITEM 067d
NEW.TYPE=T_DOOR_LOCKED
NEW.P=473,433,-1,3
SERV.NEWITEM 067f
NEW.TYPE=T_DOOR_LOCKED
NEW.P=473,432,-1,3
VAR.LESSERBOSS.C_DARKNIGHT_CREEPER=0

/////////////////////////

SERV.NEWITEM i_doom_gauntlet_floor
REF1.TAG.NEXTDOOMFLOOR=<NEW>
NEW.COLOR=33
NEW.P=468,493,-1,3
NEW.TAG.GOP=468,501,-1,3
NEW.TAG.LESSERBOSS=C_FLESH_RENDERER
NEW.TAG.LESSERBOSS.P=478,517,3,3
NEW.TAG.PENTAGRAMP=432,430,-1,3
REF2=<NEW> //second boss floor
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=468,497,-1,3
NEW.TAG.LESSERBOSS=C_FLESH_RENDERER
NEW.MOREP=468,490,-1,3
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=469,497,-1,3
NEW.TAG.LESSERBOSS=C_FLESH_RENDERER
NEW.MOREP=468,490,-1,3
SERV.NEWITEM i_door_metal
NEW.TYPE=T_DOOR_LOCKED
NEW.P=468,496,-1,3
SERV.NEWITEM 0677
NEW.TYPE=T_DOOR_LOCKED
NEW.P=469,496,-1,3
VAR.LESSERBOSS.C_FLESH_RENDERER=-1

////////////////////////////

SERV.NEWITEM i_doom_gauntlet_floor
REF2.TAG.NEXTDOOMFLOOR=<NEW>
NEW.COLOR=33
NEW.P=408,502,-1,3
NEW.TAG.GOP=409,509,-1,3
NEW.TAG.LESSERBOSS=C_IMPALER
NEW.TAG.LESSERBOSS.P=408,531,-1,3
NEW.TAG.PENTAGRAMP=424,439,-1,3
REF3=<NEW> //third boss floor
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=408,505,-1,3
NEW.TAG.LESSERBOSS=C_IMPALER
NEW.MOREP=408,499,-1,3
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=409,505,-1,3
NEW.TAG.LESSERBOSS=C_IMPALER
NEW.MOREP=408,499,-1,3
SERV.NEWITEM i_door_metal
NEW.TYPE=T_DOOR_LOCKED
NEW.P=408,504,-1,3
SERV.NEWITEM 0677
NEW.TYPE=T_DOOR_LOCKED
NEW.P=409,504,-1,3
VAR.LESSERBOSS.C_IMPALER=-1

////////////////////////////

SERV.NEWITEM i_doom_gauntlet_floor
REF3.TAG.NEXTDOOMFLOOR=<NEW>
NEW.P=360,476,-1,3
NEW.COLOR=33
NEW.TAG.GOP=361,483,-1,3
NEW.TAG.LESSERBOSS=C_SHADOW_KNIGHT
NEW.TAG.LESSERBOSS.P=338,506,-1,3
NEW.TAG.PENTAGRAMP=415,435,-1,3
REF4=<NEW> //fourth boss floor
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=360,479,-1,3
NEW.TAG.LESSERBOSS=C_SHADOW_KNIGHT
NEW.MOREP=362,472,-1,3
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=361,479,-1,3
NEW.TAG.LESSERBOSS=C_SHADOW_KNIGHT
NEW.MOREP=362,472,-1,3
SERV.NEWITEM i_door_metal
NEW.TYPE=T_DOOR_LOCKED
NEW.P=360,478,-1,3
SERV.NEWITEM 0677
NEW.TYPE=T_DOOR_LOCKED
NEW.P=361,478,-1,3
VAR.LESSERBOSS.C_SHADOW_KNIGHT=-1

////////////////////////////

SERV.NEWITEM i_doom_gauntlet_floor
REF4.TAG.NEXTDOOMFLOOR=<NEW>
NEW.P=361,430,-1,3
NEW.COLOR=33
NEW.TAG.GOP=354,429,-1,3
NEW.TAG.LESSERBOSS=C_TRUE_ABYSSMAL_HORROR
NEW.TAG.LESSERBOSS.P=328,429,-1,3
NEW.TAG.PENTAGRAMP=417,426,-1,3
NEW.TAG.DARKFATHERSUMMONER=1
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=358,429,-1,3
NEW.TAG.LESSERBOSS=C_TRUE_ABYSSMAL_HORROR
NEW.MOREP=366,429,-1,3
SERV.NEWITEM i_doom_gauntlet_floor_exit
NEW.P=358,428,-1,3
NEW.TAG.LESSERBOSS=C_TRUE_ABYSSMAL_HORROR
NEW.MOREP=366,429,-1,3
SERV.NEWITEM 067d
NEW.TYPE=T_DOOR_LOCKED
NEW.P=360,430,-1,3
SERV.NEWITEM 067f
NEW.TYPE=T_DOOR_LOCKED
NEW.P=360,429,-1,3
VAR.LESSERBOSS.C_TRUE_ABYSSMAL_HORROR=-1

//////End of Lesser Bosses creation///////

SERV.NEWITEM=067d
NEW.TYPE=T_DOOR_LOCKED
NEW.P=393,405,-1,3
SERV.NEWITEM=067f
NEW.TYPE=T_DOOR_LOCKED
NEW.P=393,404,-1,3
SERV.NEWITEM i_moongate_blue
NEW.P=391,400,-1,3
NEW.MOREP=395,405,-1,3
SERV.NEWITEM i_worldgem_bit
NEW.P=369,381,0,3
NEW.MORE1=C_H_HEALER
NEW.MORE2=1
NEW.MOREP=10,20,5
NEW.TIMERD=1
SERV.NEWITEM i_moongate_blue
NEW.P=433,371,-1,3
NEW.MOREP=479,371,-1,3
NEW.ATTR=ATTR_MOVE_NEVER|ATTR_INVIS
SERV.NEWITEM i_moongate_blue
NEW.P=433,372,-1,3
NEW.MOREP=479,371,-1,3
NEW.ATTR=ATTR_MOVE_NEVER|ATTR_INVIS
SERV.NEWITEM i_moongate_blue
NEW.P=479,371,-1,3
NEW.MOREP=430,371,-1,3
NEW.ATTR=ATTR_MOVE_NEVER|ATTR_INVIS
SERV.NEWNPC=c_h_mageshop
NEW.P=492,371,-1,3
NEW.HOME=492,371,-1,3
NEW.HOMEDIST=4
NEW.FIX

SYSMESSAGE @55 Doom generated.
SERV.LOG Doom generated
VAR.GENERATEDDOOM=1
ENDIF

[CHARDEF c_sylvana_sorceress]
ID=c_woman
NAME=Victoria, the sorceress
DEFNAME=c_sylvana_sorceress
CAN=MT_NONMOVER
CATEGORY=Addon
SUBSECTION=Doom
DESCRIPTION=Sylvana
DESIRES=i_reag_daemon_bone

on=@create
FLAGS |= statf_invul
title=the sorceress
str=100
dex=100
int=100
color=00
ITEMNEWBIE=i_robe
COLOR=01
ITEMNEWBIE=i_hair_long

on=@gethit
return 1

on=@spelleffect
return 1

ON=@ReceiveItem
IF (<LEVEL>)
SAY @55 Wait, cause there's already someone fighting the daemon.
ELIF !(<TAG0.<SRC>> >= 10000)
TRY TAG.<SRC>=<EVAL <TAG0.<SRC>>+<ARGO.AMOUNT>>
TRY UID.<ARGO.UID>.REMOVE
ENDIF
IF (<TAG0.<SRC>> >= 10000)
ANIM 16
SAY @33 Ah Kum Nah Tur
TIMERF 3,SAY @33 The daemon is right there in that stone circle.
SERV.NEWNPC c_bone_demon
NEW.P=442,204,-3,3
NEW.HOME=442,204,-3,3
NEW.HOMEDIST=4
NEW.EXP=<UID>
NEW.EVENTS +e_questsylvana_daemon
LEVEL=<SRC>
NEW.REMOVETIMER=<EVAL 60*5>
SRC.SYSMESSAGE @55 You have 5 minutes to kill the daemon.
TRY TAG.<SRC>
ENDIF
RETURN 1

ON=@NpcLooKAtChar
IF (<SRC.ISPLAYER>)
IF !(<SRC.FINDID.i_golden_skull>)
SDIALOG D_DOOM_SYLVANA
ENDIF
ENDIF

[EVENTS e_questsylvana_daemon]
ON=@Death
SERV.NEWITEM i_golden_skull
NEW.ATTR=ATTR_NEWBIE
TRY UID.<UID.<EXP>.LEVEL>.BOUNCE <NEW>
TRY UID.<UID.<EXP>.LEVEL>.SYSMESSAGE @55 Victoria: Well done! Now go to the dock to the South and ring the bell.
TRY UID.<EXP>.LEVEL

[DIALOG D_DOOM_SYLVANA]
200,180
PAGE 1
resizepic 20 20 3500 350 170
checkertrans 20 20 350 170
dhtmlgump 60 40 280 120 1 1 <DEF.BFONT_DGREEN>Long time ago I fought a being named "Dark Father", but I wasn't able to kill it and now I'm cursed to wander these halls for all eternity.<DEF.BR>This monster guards a great and powerfull treasure and those who defeat it can have his treasure.<DEF.BR>The Dark Father is on the other side of Doom and to get there you'll need to talk with Chyloth, the commander of the boat that leads to the other side. For him to let you cross the lake you'll need a "Golden Skull" that can only be found by killing a certain daemon.<DEF.BR> I can summon this daemon if you bring me 10000 Daemon Bones, each monster in Doom can give Daemon Bones, acording to it's difficulty.<DEF.BFONTE>

[DIALOG D_DOOM_SYLVANA BUTTON]
ON=0
SRC.EVENTS +e_emdoom

[EVENTS e_emdoom]
ON=@Kill
IF (<ARGO.NPC>)
IF (STRMATCH(*Doom*,<ARGO.REGION.NAME>))
SERV.NEWITEM i_reag_daemon_bone,<eval (<ARGO.maxhits>+<ARGO.dex>+<ARGO.int>)/10>
TRY UID.<ARGO.UID>.BOUNCE <NEW>
ENDIF
IF !(STRCMPI(Doom Gauntlet,<ARGO.REGION.NAME>))
IF (<R<TAG0.LUCK>> > <R50,450>) || (<ISGM>)
SERV.NEWITEM <HVAL <DEF.DOOMGAUNTLET_ARTIFACTS>>
TRY UID.<ARGO.UID>.BOUNCE <NEW>
ENDIF
ENDIF
ENDIF

[ITEMDEF i_golden_skull]
ID=i_bone_skull
NAME=golden skull
TYPE=T_NORMAL
TAG.NOVENDOR=1

ON=@Create
COLOR=084d

[ITEMDEF i_bell_doom]
ID=i_beehive
TYPE=T_NORMAL
NAME=Dead Bell
TAG.NOVENDOR=1

ON=@Dclick
IF !(<SRC.FINDID.i_golden_skull>)
SRC.SYSMESSAGE @55 Chyloth: Get out of here, you don't look strong enough to cross to the other side.
SERV.NEWNPC c_skeletal_dragon
NEW.P=420,252,0,3
NEW.HOME=420,252,0,3
NEW.HOMEDIST=10
NEW.REMOVETIMER=<EVAL 60*5>
NEW.UPDATE
ELSE
SERV.NEWNPC c_chyloth
NEW.P=417,247,4,3
NEW.FLAGS |= statf_invul|statf_dead
NEW.SDIALOG D_CHYLOTH_BELL
NEW.UPDATE
NEW.SAY @55 Only the dead shall pass to the other side <SRC.NAME>!
NEW.REMOVETIMER=20
ENDIF
RETURN 1

[CHARDEF C_CHYLOTH]
ID=C_MAN
CAN=MT_NONMOVER|MT_EQUIP|MT_USEHANDS
NAME=Chyloth
CATEGORY=Doom
SUBSECTION=Chyloth
DESCRIPTION=Chyloth

ON=@Create
STR=1
INT=1
STR=1
COLOR=01
ITEMNEWBIE=i_deathshroud
COLOR=36

[DIALOG D_CHYLOTH_BELL]
200,180
PAGE 1
resizepic 10 20 3500 290 140
checkertrans 10 20 290 140
dtext 30 40 0ad Do you wish to go to the other side with
dtext 30 60 0ad those who are in party with you?
button 30 120 249 248 1 0 1
button 210 120 243 241 1 0 0

[DIALOG D_CHYLOTH_BELL BUTTON]
ON=0
REMOVE

ON=1
IF (<DISTANCE> <= 9)
IF (<SRC.FINDID.i_golden_skull>)
IF (<SRC.ISINPARTY>)
FOR 0 <EVAL <SRC.PARTY.MEMBERS>-1>
IF (<SRC.PARTY.MEMBER.<dLOCAL._FOR>.DISTANCE <SRC>> <= 9)
REF1=<SRC.PARTY.MEMBER.<dLOCAL._FOR>.UID>
IF (<REF1> != <SRC>)
REF1.GO 424,329,0,3
ENDIF
ENDIF
ENDFOR
ENDIF
SRC.GO 424,329,0,3
SRC.CONSUME i_golden_skull
ENDIF
ENDIF
REMOVE

/////////////////////////////
//////Chefes////////


[ITEMDEF i_bleeding_timer]
ID=i_memory
TYPE=T_EQ_SCRIPT
NAME=bleeding

ON=@Timer
CONT.DAMAGE <R3,10> DAM_GOD <LINK>
MORE1 -= 1
IF !(<MORE1>)
REMOVE
ELSE
TIMER=5
ENDIF
RETURN 1

[NAMES NAMES_SHADOW_KNIGHT]
20
Oghmus
Arametheus
Terxor
Erdok
Archatrix
Jonar
Marth'Fador
Helzigar
Tyrnak
Krakus
Marcus Fel
Lord Kaos
Doomor
Uhn
Malashim
Samael
Nelokhiel
Montobulus
Usuhl
Zul

////////////////Gauntlet Control///////////////////////

[FUNCTION DOOM_HASTHISPLAYER_INSIDE]
FOR <TAG0.TOTALPLAYERS>
	IF (<TAG0.PLAYER.<dLOCAL._FOR>> == <ARGN1>)
	RETURN 1
	ENDIF
ENDFOR
RETURN 0

[FUNCTION F_RESETDOOMGAUNTLET]
FORINSTANCES i_doom_gauntlet_floor
TRY VAR.LESSERBOSS.<TAG0.LESSERBOSS>=-1
	FOR <TAG0.TOTALPLAYERS>
	TRY TAG.PLAYER.<dLOCAL._FOR>=
	ENDFOR
TAG.TOTALPLAYERS
TAG.3PLAYERTIER
COLOR=33
REF1=<TAG0.PENTAGRAM.UID>
REF1.REMOVE
TAG.PENTAGRAM.UID
	IF (<TAG0.LESSERBOSS> == C_DARKNIGHT_CREEPER)
	VAR.LESSERBOSS.C_DARKNIGHT_CREEPER=0
	COLOR=0ad
	ENDIF
ENDFOR

[ITEMDEF i_doom_gauntlet_floor_exit]
ID=i_paver_stone
NAME=Doom

ON=@Create
COLOR=0ad
ATTR=ATTR_MOVE_NEVER

ON=@Step
IF ((<SRC.FLAGS>&STATF_DEAD) || ((!(<SRC.ISPLAYER>)) && (<SRC.MEMORYFINDTYPE.MEMORY_IPET.LINK.ISPLAYER>))) || (<VAR0.LESSERBOSS.<TAG0.LESSERBOSS>> == -1)
SRC.GO <MOREP>
ELSE
SRC.SYSMESSAGE @04ac,3,1 You can not leave this room!
ENDIF

[ITEMDEF i_doom_gauntlet_floor]
ID=i_paver_stone
NAME=Doom

ON=@Create
COLOR=0ad
ATTR=ATTR_MOVE_NEVER

ON=@Step
IF (<SRC.ISPLAYER>)
	IF (<VAR0.LESSERBOSS.<TAG0.LESSERBOSS>> == -1)
	MESSAGE @04ac,3,1 This room is closed!
	ELIF (<DOOM_HASTHISPLAYER_INSIDE <SRC>>)
	SRC.GO <TAG0.GOP>
	ELSE
	TAG0.TOTALPLAYERS += 1
	TRY TAG.PLAYER.<dTAG0.TOTALPLAYERS>=<SRC>
	TAG0.3PLAYERTIER += 1
		IF (<TAG0.3PLAYERTIER> >= 3) || (<TAG0.TOTALPLAYERS> == 1)
		SERV.NEWNPC=<TAG0.LESSERBOSS>
		NEW.P=<TAG0.LESSERBOSS.P>
		NEW.HOME=<NEW.P>
		NEW.HOMEDIST=12
		NEW.UPDATEX
		TRY VAR0.LESSERBOSS.<TAG0.LESSERBOSS>=<EVAL <VAR0.LESSERBOSS.<TAG0.LESSERBOSS>>+1>
		NEW.EVENTS +e_doom_lesser_boss
		NEW.TAG.DOOMFLOOR=<UID>
			IF (<TAG0.TOTALPLAYERS> != 1)
			TAG.3PLAYERTIER
			ENDIF
		ENDIF
	SRC.GO <TAG0.GOP>
	ENDIF
ELSE
	IF (<VAR0.LESSERBOSS.<TAG0.LESSERBOSS>> != -1)
	SRC.GO <TAG0.GOP>
	ENDIF
ENDIF

[EVENTS e_doom_lesser_boss]
ON=@Death
LOCAL.NEW=<NEW>
IF (<ATTACKER>)
	FOR y 0 <EVAL <ATTACKER>-1>
		IF (<ATTACKER.<dLOCAL.Y>>)
			IF (<ATTACKER.<dLOCAL.Y>.DAM> >= <EVAL (<MAXHITS>/16)>) || (<ATTACKER.<dLOCAL.Y>.ISGM>)
			REF402=<ATTACKER.<dLOCAL.Y>>
				IF (<REF402.FLAGS>&STATF_PET)
				REF402=<REF402.MEMORYFINDTYPE.memory_ipet.LINK>
				ENDIF
			REF402.NEWGOLD 2000
				IF (<R<REF402.LUCK>> > <R750>) || (<REF402.ISGM>)
				SERV.NEWITEM <HVAL <DEF.DOOMGAUNTLET_ARTIFACTS>>
				REF402.BOUNCE <NEW>
				REF402.sysmessage @,,2 1062317
				ENDIF
			ENDIF
		ENDIF
	ENDFOR
ENDIF
NEW=<LOCAL.NEW>

REF1=<TAG.DOOMFLOOR> //Doom Floor that originated this lesser boss
LOCAL.TOTALPLAYERS=<REF1.TAG0.TOTALPLAYERS>
TRY VAR0.LESSERBOSS.<BASEID>=<EVAL <VAR0.LESSERBOSS.<BASEID>>-1>

IF (<VAR0.LESSERBOSS.<BASEID>> <= 0)
TRY VAR.LESSERBOSS.<BASEID>=-1
REF2=<REF1.TAG0.NEXTDOOMFLOOR>
	IF (<REF2>)
	TRY VAR.LESSERBOSS.<REF2.TAG0.LESSERBOSS>=0
	REF2.COLOR=0ad
	ENDIF
	FOR <REF1.TAG0.TOTALPLAYERS>
	TRY REF1.TAG.PLAYER.<dLOCAL._FOR>=
	ENDFOR
REF1.TAG.TOTALPLAYERS=
REF1.TAG.3PLAYERTIER=
REF1.COLOR=33

SERV.NEWITEM=i_multi_pentagram
NEW.P=<REF1.TAG0.PENTAGRAMP>
NEW.MULTICREATE
REF1.TAG.PENTAGRAM.UID=<NEW>
NEW=<LOCAL.NEW>

ENDIF

IF (<REF1.TAG0.DARKFATHERSUMMONER>)
LOCAL.DARKFATHERS=<EVAL <LOCAL.TOTALPLAYERS>/3>
	IF (<LOCAL.DARKFATHERS> > 4)
	LOCAL.DARKFATHERS=4
	ENDIF
	IF (<LOCAL.DARKFATHERS> < 1)
	LOCAL.DARKFATHERS=1
	ENDIF
	FOR <LOCAL.DARKFATHERS>
	SERV.NEWNPC c_dark_father
	VAR0.DARKFATHER += 1
		DORAND 5
		NEW.P=419,430,-1,3
		NEW.P=422,426,-1,3
		NEW.P=426,428,-1,3
		NEW.P=426,434,-1,3
		NEW.P=421,434,-1,3
		ENDDO
	NEW.UPDATEX
	ENDFOR
ENDIF

//////////////////////////////////////
////////////Artefatos////////////////

[ITEMDEF i_doom_skull_candle]
ID=i_skull_candle
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=skull candle
TAG.DOOMARTIFACT=2
TYPE=T_NORMAL

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,2

[ITEMDEF i_doom_stretched_hide]
ID=i_hide_stretched
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Stretched Hide
TAG.DOOMARTIFACT=3

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,3

[ITEMDEF i_doom_lamppost]
ID=i_lamppost1
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Doom Lamppost
TAG.DOOMARTIFACT=6
TYPE=T_NORMAL

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,4

[ITEMDEF i_doom_Tower_Lantern]
ID=024bf
NAME=tower lantern
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Doom Tower Lantern
TAG.DOOMARTIFACT=8
TYPE=T_NORMAL

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,4

[ITEMDEF i_doom_fancy_wind_chimes]
ID=02833
NAME=fancy wind chimes
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Doom fancy wind chimes
TAG.DOOMARTIFACT=16
TYPE=T_NORMAL

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,6

[ITEMDEF i_doom_giftbox]
ID=9002
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Doom gift box
TAG.DOOMARTIFACT=42
TYPE=T_NORMAL

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,9

[ITEMDEF i_doom_goat_skinned]
ID=i_goat_skinned
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=skinned goat
TAG.DOOMARTIFACT=24

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,2

[ITEMDEF i_doom_cards_tarot]
ID=i_cards_tarot
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=tarot cards
TAG.DOOMARTIFACT=24

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,2

[ITEMDEF i_doom_guillotine]
ID=i_guillotine
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=guilotine
TAG.DOOMARTIFACT=53

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,6

[ITEMDEF i_doom_brazier]
ID=i_brazier
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=brazier
TAG.DOOMARTIFACT=24

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,2

[ITEMDEF i_doom_gruesomestandard]
ID=i_gruesomestandard
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=gruesomestandard
TAG.DOOMARTIFACT=24

ON=@Create
COLOR={0470 0500}

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,5

[ITEMDEF i_doom_painting_ruined]
DEFNAME=i_doom_painting_ruined
ID=i_portrait_big
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=ruined painting
TAG.DOOMARTIFACT=96

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,7

[ITEMDEF i_doom_titan_hammer]
ID=i_hammer_war
NAME=Titan's Hammer
SKILL=Macefighting
DAM=17,20
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Titan's Hammer
TAG.DOOMARTIFACT=24*28
WEIGHT=2.5
SPEED=28
TWOHANDS=Y
REQSTR=95
TAG.DAMINCREASE=50
TAG.HITCHANCEINCREASE=15
TEVENTS=T_ORB_CLILOC
TEVENTS=T_PROPRIEDADES
TEVENTS=t_luck_item
TAG.LUCK=50

ON=@Create
TAG.ORB.EFEITO=01000
COLOR=color_magic_invulnerability
HITPOINTS=255

ON=@Equip
SRC.MODSTR += 15

ON=@UneQuip
SRC.MODSTR -= 15

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,10
SRC.ADDCLILOC 1060485,15

[ITEMDEF i_doom_inquisitor_resolution]
DEFNAME=i_doom_inquisitor_resolution
ID=i_platemail_gloves
NAME=Inquisitor's Resolution
ARMOR=60
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Inquisitor's Revolution
TAG.DOOMARTIFACT=24*28
WEIGHT=2
REQSTR=15
TEVENTS=t_luck_item
TAG.LUCK=50

ON=@Create
COLOR=0122
HITPOINTS=255
ATTR=ATTR_MAGIC
MOREY=800

ON=@ClientTooltip
SRC.ADDCLILOC 1061078,10
SRC.ADDCLILOC 1060451,Magery,10
SRC.ADDCLILOC 1060452,Evaluate Intel,10
SRC.ADDCLILOC 1060453,Magic Resistance,10
SRC.ADDCLILOC 1060432,10

ON=@Equip
SRC.MODINT += 10
SRC.MAGERY += 10.0
SRC.EVALUATINGINTEL += 10.0
SRC.MAGICRESISTANCE += 10.0

ON=@UneQuip
SRC.MODINT -= 10
SRC.MAGERY -= 10.0
SRC.EVALUATINGINTEL -= 10.0
SRC.MAGICRESISTANCE -= 10.0

///////////////////////////

[FUNCTION DOOMLOCATION]
P=<eval {269 492}>,<eval {6 242}>,0,3
FIX
IF (<isneartype t_rock 1>) || (<isneartype t_tree 1>) || (<isneartype t_door 1>) || (<isneartype t_wall 2>) || (<serv.map(<p.x>,<p.y>,<map>).statics.0.id>==01d0) || (<isneartype t_window>) || (<z> >= 2) || (<P.TERRAIN>==0244)
DOOMLOCATION
RETURN 1
ENDIF

//////////////////////////

[ITEMDEF i_doomartifact_spawn]
ID=i_worldgem_bit
TYPE=T_NORMAL
NAME=Doom Artifact Spawn
CATEGORY=Doom
SUBSECTION=Artefatos
DESCRIPTION=Spawner

ON=@Create
ATTR=ATTR_INVIS|ATTR_MOVE_NEVER

ON=@Timer
SERV.NEWITEM <MORE1>
NEW.P=<P>
NEW.ATTR |= ATTR_MOVE_NEVER
REMOVE
RETURN 1

////////////////////////////////////
//////////Dark Guardians///////////

/////////////////////////////

[ITEMDEF i_poison_fog_creator]
ID=i_worldgem_bit
NAME=Poison Fog Creator
TYPE=T_NORMAL

ON=@Create
ATTR=ATTR_INVIS|ATTR_MOVE_NEVER

ON=@Timer
FOR 15
SERV.NEWITEM i_poison_gas_darkguard
NEW.P=<P>
NEW.MOVE <EVAL {-9 9}> <EVAL {-9 9}>
NEW.MORE1=<MORE1>
NEW.ATTR=ATTR_DECAY|ATTR_MOVE_NEVER|ATTR_CAN_DECAY
NEW.TIMER=25
NEW.FIX
ENDFOR
TIMER=26
MORE1 += 100
RETURN 1

[ITEMDEF i_poison_gas_darkguard]
ID=i_trap_gas_3
TYPE=T_FLOOR
NAME=poison gas

ON=@Step
IF (<SRC.ISPLAYER>)
SRC.SPELLEFFECT S_POISON,<MORE1>,<UID>
ENDIF

[REGIONTYPE R_DARKGUARDIAN_ROOM]
ON=@Enter
IF (<SRC.ISPLAYER>)
IF !(<SRC.ISGM>)
IF !(<VAR0.DARKGUARDIANS>) && !(<VAR0.DARKGUARDIANCONTROLER>)
SRC.TRY UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER
SRC.TRY UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER
SRC.TIMERF 2,UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER -1
SRC.TIMERF 2,UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER -1
SRC.REGION.ALLCLIENTS DARK_GUARDIAN_ROOM_SET
SERV.NEWITEM i_poison_fog_creator
NEW.P=365,15,-1,3
NEW.MORE1=300
NEW.TIMER
ENDIF
ENDIF
ENDIF

[REGIONTYPE R_DARKGUARDIAN_ROOM_BLOCK]
ON=@Enter
IF (<SRC.ISPLAYER>)
IF !(<SRC.ISGM>)
IF (<VAR0.DARKGUARDIANCONTROLER>) || (<SRC.FLAGS>&statf_dead)
IF !(<SRC.TAG0.EVENT.ID>)
SRC.UPDATE
RETURN 1
ENDIF
ENDIF
ENDIF
ENDIF

ON=@Exit
IF (<SRC.ISPLAYER>)
IF !(<SRC.ISGM>)
IF (<SRC.TAG0.EVENT.ID>)
IF !(<VAR0.DARKGUARDIANS>)
SRC.SAIRDARKGUARDIAN
ENDIF
ENDIF
ENDIF
ENDIF

[FUNCTION DARK_GUARDIAN_ROOM_SET]
FOR 2
SERV.NEWNPC c_dark_guardian
DORAND 4
NEW.P=365,9,-1,3
NEW.P=369,14,-1,3
NEW.P=366,22,-2,3
NEW.P=358,22,-1,3
ENDDO
NEW.EVENTS +e_dark_guardian_room_npc
NEW.UPDATE
NEW.ATTACK <UID>
VAR0.DARKGUARDIANS += 1
ENDFOR
TAG.EVENT.ID=200
EVENTS +e_darkguard_player
VAR0.DARKGUARDIANPLAYERS += 1

[EVENTS e_dark_guardian_room_npc]
ON=@Death
VAR0.DARKGUARDIANS -= 1
IF !(<VAR0.DARKGUARDIANS>)
VAR.DARKGUARDIANS
SERV.NEWITEM i_chest_metal
NEW.P=365,15,-1,3
NEW.ATTR=ATTR_DECAY|ATTR_MOVE_NEVER|ATTR_CAN_DECAY
NEW.TIMER=59
LOCAL.CHEST=<NEW>
SERV.NEWITEM i_gold,{500 700},<LOCAL.CHEST>
FOR <R1,3>
DORAND 2
SERV.NEWITEM {i_double_blade 1 i_lance 1 i_scyth 1 i_bone_harvester 1 i_scepter 1 i_bladed_staff 1 i_pike 1 i_double_bladed_staff 1 i_nodachi 1 i_daicho 1 i_sai 1 i_tekagi 1 i_horseman_bow 1 i_composite_bow 1 i_yumi 1}
SERV.NEWITEM {i_sword_viking 1 i_dagger 1 i_mace 1 i_maul 1 i_kryss 1 i_bow 1 i_bracelet_gold 1 i_spear 1 i_spear_short 1}
ENDDO
NEW.EVENTS +T_PROPRIEDADES
IF (STRMATCH(*t_weapon*,<NEW.TYPE>))
NEW.TAG.DAMINCREASE=<R3,11>
IF !(<R2>)
NEW.TAG.SWINGINCREASE=<R3,11>
ENDIF
IF !(<R2>)
NEW.TAG.HITCHANCEINCREASE=<R2,11>
ENDIF
ENDIF
IF !(<R5>) || (<NEW.TYPE>==t_jewelry)
NEW.EVENTS +T_LUCK_ITEM
NEW.TAG.LUCK=<R10,30>
ENDIF
NEW.CONT=<LOCAL.CHEST>
ENDFOR
TRY UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER
TRY UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER
ACT.TIMERF 1,UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER -1
ACT.TIMERF 2,UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER -1
SERV.NEWITEM i_darkguardian_room_controler
NEW.P=365,15,-1,3
NEW.TIMER=60
VAR.DARKGUARDIANCONTROLER=1
SERV.NEWITEM i_darkguardian_room_controler
NEW.P=358,15,-1,3
NEW.TIMER=60
VAR.DARKGUARDIANCONTROLER=2
ENDIF

[EVENTS e_darkguard_player]
ON=@DeathCorpse
TAG.DARKGUARDBODY=<ARGO.UID>

ON=@LogOut
HITS=1
FORCHARS 20
IF (<BASEID>==C_DARK_GUARDIAN)
ATTACK <SRC>
ENDIF
ENDFOR

ON=@Death
SAIRDARKGUARDIAN 1

[ITEMDEF i_darkguardian_room_controler]
ID=i_worldgem_bit
NAME=Dark Guardian Room Controler
TYPE=T_NORMAL

ON=@Create
ATTR=ATTR_INVIS|ATTR_MOVE_NEVER

ON=@Timer
REGION.ALLCLIENTS SAIRDARKGUARDIAN 1
REGION.ALLCHARSIDLE SAIRDARKGUARDIAN 1
VAR0.DARKGUARDIANCONTROLER -= 1
IF (<VAR0.DARKGUARDIANCONTROLER> < 1)
VAR.DARKGUARDIANCONTROLER
ENDIF
FORCHARS 15
IF (<FLAGS>&statf_pet)
IF !(STRCMPI(<REGION.NAME>,<OBJ.REGION.NAME>))
GO 354,14,-1,3
ENDIF
ENDIF
ENDFOR
REMOVE
RETURN 1

[FUNCTION SAIRDARKGUARDIAN]
IF (<ISPLAYER>)
IF (<TAG0.EVENT.ID>)
VAR0.DARKGUARDIANPLAYERS -= 1
IF !(<VAR0.DARKGUARDIANPLAYERS>)
IF (<ARGN1>)
IF (<VAR0.DARKGUARDIANS>)
TRY UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER
TRY UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER
TIMERF 2,UID.<VAR0.DARK_GUARDIAN_DOOR1>.TIMER -1
TIMERF 2,UID.<VAR0.DARK_GUARDIAN_DOOR2>.TIMER -1
ENDIF
VAR.DARKGUARDIANCONTROLER
ENDIF
VAR.DARKGUARDIANS
FORITEMS 15
IF (<BASEID>==i_poison_fog_creator) || (<BASEID>==i_poison_gas_darkguard)
REMOVE
ENDIF
ENDFOR
FORCHARS 20
IF (<BASEID>==C_DARK_GUARDIAN)
REMOVE
ENDIF
ENDFOR
VAR.DARKGUARDIANPLAYERS
ENDIF
EVENTS -e_darkguard_player
TAG.EVENT.ID
IF (<ARGN1>)
IF (<TAG0.DARKGUARDBODY>)
TRY UID.<TAG0.DARKGUARDBODY>.REMOVEFROMVIEW
TRY UID.<TAG0.DARKGUARDBODY>.P=495,51,-1,3
TRY UID.<TAG0.DARKGUARDBODY>.UPDATE
ENDIF
GO 495,51,-1,3
ENDIF
TAG.DARKGUARDBODY
EVENTS -e_darkguard_player
ENDIF
ENDIF

[AREADEF a_doom_darkguard_room]
NAME=Doom (suspicious room)
P=365,15,-1,3
Rect=355,5,376,26,3
EVENTS=R_DARKGUARDIAN_ROOM_BLOCK
FLAGS=region_flag_nobuilding|region_antimagic_recall_in|region_antimagic_gate|region_antimagic_recall_out|region_flag_underground|region_antimagic_teleport
GROUP=Malas

[AREADEF a_doom_darkguardian_room]
NAME=Dark Guardian Room
P=365,15,-1,3
Rect=360,4,376,26,3
EVENTS=R_DARKGUARDIAN_ROOM
FLAGS=region_flag_nobuilding|region_antimagic_recall_in|region_antimagic_gate|region_antimagic_recall_out|region_flag_underground|region_antimagic_teleport
GROUP=Malas

///////////////////////////////////////
/////////////Lamp Puzzle//////////////

[TYPEDEF T_LAMP_LEVER]
ON=@Dclick
IF (<LINK.TIMER>==-1)
IF (<TIMER>==-1)
IF (<SRC.P.X> == <MOREX>) && (<SRC.P.Y> == <MOREY>)
LINK.TAG.ATUAL .= <EVAL <MORE1>>.
TIMER=5
DISPID=0108e
UPDATE
IF (<EVAL STRLEN(<LINK.TAG.ATUAL>)>==8)
IF (<LINK.TAG.ATUAL>!=<LINK.TAG.CORRETO>)
LINK.DOOMLAMPERRO
ELSE
LINK.DOOMLAMPCERTO
ENDIF
ENDIF
ELSE
SRC.SYSMESSAGE @55 Stay on the square.
ENDIF
ENDIF
ENDIF
RETURN 1

ON=@Timer
LINK.TAG.ATUAL
LINK.DOOMLAMPRESET
RETURN 1

[FUNCTION DOOMLAMPCERTO]
LOCAL.TMP=<OBJ>
OBJ=<UID>
FORCLIENTS 0
IF (<OBJ.TIMER>==-1)
LOCAL.1=<R1,5>
LOCAL.2=<R1,5>
WHILE (<LOCAL.2> == <LOCAL.1>)
LOCAL.2=<R1,5>
ENDWHILE
LOCAL.3=<R1,5>
WHILE ((<LOCAL.3> == <LOCAL.1>) || (<LOCAL.3> == <LOCAL.2>))
LOCAL.3=<R1,5>
ENDWHILE
LOCAL.4=<R1,5>
WHILE ((<LOCAL.4> == <LOCAL.1>) || (<LOCAL.4> == <LOCAL.2>) || (<LOCAL.4> == <LOCAL.3>))
LOCAL.4=<R1,5>
ENDWHILE
OBJ.TAG.CORRETO <EVAL <LOCAL.1>>.<EVAL <LOCAL.2>>.<EVAL <LOCAL.3>>.<EVAL <LOCAL.4>>
OBJ.TIMER=<EVAL 60*10>
OBJ.COLOR=33
GO 468,96,-1,3
EVENTS +e_lamproom
SERV.NEWITEM=i_lamproom_sair
EQUIP <NEW>
NEW.TIMER=<EVAL 60*5>
ENDIF
ENDFOR
OBJ=<LOCAL.TMP>

[FUNCTION DOOMLAMPERRO]
DOOMLAMPRESET
FORCLIENTS 20
SERV.NEWITEM i_boulder
NEW.P=<P>
NEW.MOVE W
NEW.ATTR=ATTR_DECAY|ATTR_MOVE_NEVER|ATTR_CAN_DECAY
NEW.TIMER=5
DAMAGE 70 01
SYSMESSAGE @36 Errado!
ENDFOR

[FUNCTION DOOMLAMPRESET]
LOCAL.TMP=<OBJ>
FOR 4
OBJ=<TAG0.DOOM.LEVER<EVAL <LOCAL._FOR>>>
OBJ.TIMER -1
OBJ.DISPID=i_lever
OBJ.UPDATE
ENDFOR
TAG.ATUAL
OBJ=<LOCAL.TMP>

[EVENTS e_lamproom]
ON=@Death
FINDID.i_lamproom_sair.REMOVE
REMOVEFROMVIEW
MOVETO 428,233,-1,3
EVENTS -e_lamproom
UPDATE

ON=@Kill
IF !(<ARGO.ISPLAYER>)
LOCAL.TMP=<OBJ>
OBJ=<ARGO.UID>
OBJ.GO 428,233,-1,3
FINDID.i_lamproom_sair.REMOVE
REMOVEFROMVIEW
MOVETO 428,233,-1,3
EVENTS -e_lamproom
UPDATE
ENDIF
OBJ=<LOCAL.TMP>

[TYPEDEF T_CHAO_SAIRLAMPROOM]
ON=@Step
SRC.FINDID.i_lamproom_sair.REMOVE
SRC.GO 429,233,-1,3
SRC.EVENTS -e_lamproom

[TYPEDEF T_CHAO_LAMPROOM]
ON=@Timer
COLOR=0ad
RETURN 1

[TYPEDEF T_CAIXA_DOOMLAMP]
ON=@Dclick
IF (<TIMER>==-1)
SERV.NEWNPC c_wanderer_void
NEW.P=470,96,-1,3
NEW.UPDATE
NEW.ATTACK <SRC>
NEW.REMOVETIMER=<EVAL 60*5>
TIMER=<EVAL 60*5>
ENDIF
RETURN 1

ON=@Timer
RETURN 1

ON=@Dropon_Self
IF !(<SRC.ISGM>)
RETURN 1
ENDIF

[ITEMDEF i_lamproom_sair]
ID=i_memory
TYPE=T_EQ_SCRIPT
NAME=laproom sair

ON=@Timer
CONT.GO 429,233,-1,3
CONT.EVENTS -e_lamproom
REMOVE
RETURN 1



[TYPEDEF t_luck_item]
ON=@Equip
SRC.LUCK += <TAG0.LUCK>

ON=@UnEquip
SRC.LUCK -= <TAG0.LUCK>

ON=@ClientToolTip
SRC.ADDCLILOC 1060436,<EVAL <TAG0.LUCK>>

[TYPEDEF T_PROPRIEDADES]

[TYPEDEF T_ORB_CLILOC]
ON=@ClientTooltip
SHOWORBDAMAGES

[FUNCTION SHOWORBDAMAGES]
IF (<TAG0.ORB.EFEITO>)
IF (<TAG0.ORB.EFEITO>&01)
LOCAL.DAMAGETYPES .= God Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&08)
LOCAL.DAMAGETYPES .= Poison Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&010)
LOCAL.DAMAGETYPES .= Fire Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&020)
LOCAL.DAMAGETYPES .= Eletric Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&0100)
LOCAL.DAMAGETYPES .= Acid Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&0200)
LOCAL.DAMAGETYPES .= Cold Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&0800)
LOCAL.DAMAGETYPES .= Piercing Damage/
ENDIF
IF (<TAG0.ORB.EFEITO>&01000)
LOCAL.DAMAGETYPES .= Area Damage/
ENDIF
LOCAL.HEHE=<EVAL <EVAL STRLEN(<LOCAL.DAMAGETYPES>)>-2>
SRC.ADDCLILOC 1070722,<STRSUB 1 <LOCAL.HEHE> <LOCAL.DAMAGETYPES>>
ENDIF

[FUNCTION removetimer]
SERV.NEWITEM i_removetimer_mem
EQUIP <NEW>
NEW.TIMER=<ARGN1>

[ITEMDEF i_removetimer_mem]
ID=i_memory
NAME=remove timer
TYPE=t_eq_script

ON=@Timer
cont.remove
return 1

[FUNCTION RANDMAGICITEM]
DORAND 4
SERV.NEWITEM CHAMP_LOOT_LOW
SERV.NEWITEM CHAMP_LOOT_MED
SERV.NEWITEM CHAMP_LOOT_HIGH
SERV.NEWITEM CHAMP_LOOT_VERY_HIGH
ENDDO
BOUNCE <NEW>

[EOF]
