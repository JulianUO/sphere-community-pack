[EVENTS e_NpcGenericEvent]
on=@attack
return <src.trigger @Attacked,<def.tat_as_argo>,<uid>>

// Followers (Death)
on=@death
forcharmemorytype memory_fight
color |= <def.memory_harmedby>
endfor
if <def.scp.followers> == 1
	if <src.memoryfindtype.memory_ipet>
		if !<src.tag0.summonfail>
		ref7 = <src.memoryfindtype.memory_ipet.link.uid>
		ref7.curfollower -= <qval <src.tag0.control.slots> ? <src.tag0.control.slots> : 1>
		endif
	endif
endif
// Death Robe Fix
if <findlayer.22>
tag.robe=<findlayer.22.uid>
	if <findlayer.3>
	tag.feet=<findlayer.3.uid>
	endif
endif
// Quest System
ref42 = <attacker.max>
if (<ref42.tag0.quests>)
	for <ref42.tag0.quests>
		for x 1 <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objectiveamount>
			if <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_type> == 2
				if (<baseid> == <streat <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>)
					if (<ref42.tag0.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>)
						if !(<isempty <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_location>>)
							if (strmatch(*<def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>_location>*,<region.name>))
							ref42.tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>> --
							endif
						else
						ref42.tag.quest<dlocal._for>.slay<streat <def.quest_<dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>> --
						endif
						if !(<ref42.tag0.quest<dlocal._for>.slay<streat <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>)
						sysmessage <def.scp.quest_complete>
						else
						sysmessage @,,2 1075051,<ref42.dtag0.quest<dlocal._for>.slay<streat <def.quest_<ref42.dtag0.quest<dlocal._for>.id>_objective<dlocal.x>>>>
						endif
					endif
				endif
			endif
		endfor
	endfor
endif

// Quest System
on=@deathcorpse
if <tag0.robe>
uid.<tag.robe>.z = 3
uid.<tag.feet>.z = 8
tag.robe=
tag.feet=
argo.update
endif
ref94 = <attacker.max>
if <ref94.tag0.quests>
	for x 1 <ref94.tag0.quests>
		for y 1 <def.quest_<ref94.dtag0.quest<dlocal.x>.id>_objectiveamount>
			if <def.quest_<ref94.dtag0.quest<dlocal.x>.id>_objective<dlocal.y>_type> == 6
				if <baseid> == <streat <def.quest_<ref94.dtag0.quest<dlocal.x>.id>_objective<dlocal.y>>>
					if <isempty <def.quest_<ref94.dtag0.quest<dlocal.x>.id>_objective<dlocal.y>_location>>
					ref94.f_quest_gotitem <uid>,<local.x>,<local.y>
					else
						if (strmatch(<ref94.region.name>,*<def.quest_<ref94.dtag0.quest<dlocal.x>.id>_objective<dlocal.y>_location>*))
						ref94.f_quest_gotitem <uid>,<local.x>,<local.y>
						endif
					endif
				endif
			endif
		endfor
	endfor
endif

// Followers (Death?)
on=@destroy
if <def.scp.followers> == 1
	if <memoryfindtype.memory_ipet>
	ref7 = <memoryfindtype.memory_ipet.link.uid>
	ref7.curfollower -= <qval <tag0.control.slots> ? <tag0.control.slots> : 1>
	endif
endif

// Followers (Desert)
on=@petdesert
if <def.scp.followers> == 1
src.curfollower -= <qval <tag0.control.slots> ? <tag0.control.slots> : 1>
endif

// OSI Spells Bonus + Magic Reflection + Magic Resistance
// Followers (Summon)
on=@spelleffect
tag.spellhit = <argn1>
if <isdamagespell <argn1>>
	if <def.scp.Combat_SpellBonus> == 1
	tag.gothitbyspell = <argn1>
	endif
elif <serv.spell.<argn1>.flags>&spellflag_summon
	if <def.scp.followers> == 1
	local.slots = <qval <tag0.control.slots> ? <tag0.control.slots>:1>
		if <eval <src.curfollower>+<local.slots>> > <src.maxfollower>
		src.sysmessage You have too many followers
		tag.summonfail = 1
		hits = 0
		else
		src.curfollower += <local.slots>
		events +e_NpcSummoned
		endif
	endif
endif
if <flags>&statf_reflection
	if <src.uid> != <uid>
		if (<serv.spell.<argn1>.flags>&spellflag_harm) || <argn> == 41
		flags &= ~statf_reflection
		findid.i_rune_magic_reflection.remove
		effect 3,i_fx_bless_effect,16,16
			if <src.flags>&statf_reflection
			src.flags &= ~statf_reflection
			src.findid.i_rune_magic_reflection.remove
			src.effect 3,i_fx_bless_effect,16,16
			else
			src.spelleffect <argn1>,<argn2>
			endif
		return 1
		endif
	endif
endif

// Poisoning System
on=@hittry
if <def.scp.NewSkills_Poisoning> == 1
	if <poisoning> >= <def.scp.Poisoning_MonsterPoisonous>
	tag.skill.poisoning = <min <eval (<poisoning> + 1) / (<def.scp.Poisoning_MonsterPoisonous> + 1)>,6>
	endif
poisoning = 0
endif

// Poisoning System
on=@hit
if <def.scp.NewSkills_Poisoning> == 1
	if <tag0.skill.poisoning>
	src.new_poison <max <eval <tag0.skill.poisoning>-2>,0>,<uid>
	endif
endif

[eof]