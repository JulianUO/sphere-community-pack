[TEMPLATE random_necro_scroll]
CATEGORY=Item Templates
SUBSECTION=Scrolls
DESCRIPTION=Necromancer Scrolls
ITEM={ i_scroll_animate_dead 1 i_scroll_blood_oath 1 i_scroll_corpse_skin 1 i_scroll_curse_weapon 1 i_scroll_evil_omen 1 i_scroll_horrific_beast 1 i_scroll_lich_transformation 1 i_scroll_mind_rot 1 i_scroll_pain_spike 1 i_scroll_poison_strike 1 i_scroll_strangle 1 i_scroll_summon_familiar 1 i_scroll_vampiric_embrace 1 i_scroll_vengeful_spirit 1 i_scroll_wither 1 i_scroll_wraith_form 1 }

//#################################################\\
//#                 Curse Weapon                  #\\
//#################################################\\

[itemdef i_curse_weapon]
NAME=Curse Weapon
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Equip
src.events +e_curse_weapon
link=<src.tag.cursed_weapon>

ON=@UnEquip
cont.events -e_curse_weapon
cont.tag.cursed_weapon=
cont.sound snd_spell_magic_arrow

ON=@Timer
remove
return 1

[events e_curse_weapon]
ON=@Hit
if (<argo.uid>==<tag0.cursed_weapon>)
  sound 1317
  hits += <argn1>/2
  if <hits> > <maxhits>
     hits=<maxhits>
  endif
endif

//#################################################\\
//#                  Pain Spike                   #\\
//#################################################\\


//#################################################\\
//#                  Corpse Skin                  #\\
//#################################################\\

[itemdef i_corpse_skin]
NAME=Corpse Skin
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special
TEVENTS e_armor
tag.physicalresist=10
tag.coldresist=10
tag.fireresist=-10
tag.poisonresist=10

ON=@Equip
  src.color=0059f
  src.events +e_corpse

ON=@UnEquip
  cont.color=<cont.oskin>
  cont.events -e_corpse

ON=@Timer
  remove
  return 1

[events e_corpse]
ON=@Spelleffect
if (<argn> == 34) || (<argn> == 41)
  consume i_corpse_skin
endif

//#################################################\\
//#                   Evil Omen                   #\\
//#################################################\\

[events e_evil_omen]
ON=@GetHit
argn1 += <argn1>/4
events -e_evil_omen

ON=@Spelleffect
if (<argn> == 34) || (<argn> == 41)
  events -e_evil_omen
endif

//#################################################\\
//#                  Blood Oath                   #\\
//#################################################\\
//#################################################\\
//#                 Wraith Form                   #\\
//#################################################\\

[itemdef i_wraith_form]
NAME=Wraith Form
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Equip
  src.body=c_Harrower
  src.color=07fff
  src.events +e_wraith

ON=@UnEquip
  src.body=<src.obody>
  src.color=<src.oskin>
  src.events -e_wraith

[events e_wraith]
ON=@Hit
src.sound 1307
mana += <eval (<argn1>/3)*2>
src.mana -= <eval (<argn1>/3)*2>
if (<mana> > <maxmana>)
mana=<maxmana>
endif
if (<src.mana> < 0)
src.mana=0
endif

ON=@Spellcast
if (<argn>==56)
  src.sysmessage You can't do that in your current state
  return 1
endif

[itemdef i_wraith_fx]
defname=i_wraith_fx
name=wraith fx
id=i_fx_smoke

on=@create
color=00afd

//#################################################\\
//#                   Mind Rot                    #\\
//#################################################\\

[itemdef i_mind_rot]
ID=i_memory 
name=mind rot
TYPE=t_eq_script

ON=@Equip
src.events +e_mind_rot

ON=@UnEquip
cont.events -e_mind_rot

ON=@Timer
remove
return 1

[events e_mind_rot]
ON=@SpellSelect
  mana -= {5 15}
if (<src.mana> < 0)
src.mana=0
endif

ON=@Spelleffect
if (<argn> == 34) || (<argn> == 41)
  consume i_mind_rot
endif

//#################################################\\
//#                Summon Familiar                #\\
//#################################################\\

[skillmenu sm_summon_familiar]
Which familiar do you wish to summon?

ON=i_pet_hordedaemon Horde Minion
TEST=necromancy 30.0 spiritspeak 30.0
SUMMON=c_familiar_horde_minion

ON=i_pet_wisp Shadow Wisp
TEST=necromancy 50.0 spiritspeak 50.0
SUMMON=c_familiar_wisp_shadow

ON=i_pet_wolf Dark Wolf
TEST=necromancy 60.0 spiritspeak 60.0
SUMMON=c_familiar_dark_wolf

ON=i_pet_snake_giant Death Adder
TEST=necromancy 80.0 spiritspeak 80.0
SUMMON=c_familiar_death_adder

ON=i_pet_vampirebat Vampire Bat
TEST=necromancy 100.0 spiritspeak 100.0
SUMMON=c_familiar_vampire_bat

//#################################################\\
//#                 Horrific Beast                #\\
//#################################################\\

[itemdef i_beast_form]
NAME=Beast Form
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Equip
  src.body=c_Moloch
  src.color=0
  src.events +e_beast_form

ON=@UnEquip
  src.body=<src.obody>
  src.color=<src.oskin>
  src.events -e_beast_form

ON=@Timer
cont.hits += 1
if (<cont.hits> > <cont.maxhits>)
  cont.hits=<cont.maxhits>
endif
timer=1
return 1

[events e_beast_form]
ON=@Hit
argn1 += <argn1>/4

ON=@Spellcast
if !((<argn>==106) || (<argn>==107) || (<argn>==113) || (<argn>==116))
  src.sysmessage You can't do that in your current state
  return 1
endif

[itemdef i_beast_fx]
defname=i_beast_fx
name=beast fx
id=i_fx_smoke

on=@create
color=colors_neutral

//#################################################\\
//#                  Poison Strike                #\\
//#################################################\\

[itemdef i_poison_cloud]
defname=i_poison_cloud
name=Poison Cloud
id=i_fx_smoke

ON=@Create
color=00787

ON=@Step
  src.spelleffect 20 750
  src.damage <more1> dam_poison <uid>
  remove
  return 1

//#################################################\\
//#                      Wither                   #\\
//#################################################\\

//#################################################\\
//#                     Strangle                  #\\
//#################################################\\

[itemdef i_strangle]
NAME=Strangle
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Timer
  local.multiplier=(<eval (3-(<cont.stam>/<cont.maxstam>)*2>)
  local.damage=(<more2>*<local.multiplier>)
  cont.effect 3,i_fx_heal_effect,16,20,0,01,1
  cont.damage <local.damage> dam_poison <cont.uid>
  more1 -= 1
  if (<more1> < 1)
    remove
  endif
  timer=<more1>
  return 1

//#################################################\\
//#                   Liche Form                  #\\
//#################################################\\

[itemdef i_liche_form]
NAME=liche Form
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Equip
  src.body=c_liche
  src.color=0
  src.events +e_liche_form

ON=@UnEquip
  src.body=<src.obody>
  src.color=<src.oskin>
  src.events -e_liche_form

ON=@Timer
cont.hits -= 5
cont.mana += 13
if (<cont.mana> > <cont.maxmana>)
  cont.mana=<cont.maxmana>
endif
timer=10
return 1

[events e_liche_form]
ON=@Spellcast
if (<argn>==56)
  src.sysmessage You can't do that in your current state
  return 1
endif

[itemdef i_liche_fx]
defname=i_liche_fx
name=liche fx
id=i_fire_column
type=t_script

on=@create
color=001

//#################################################\\
//#                 Vengeful Spirit               #\\
//#################################################\\

[ITEMDEF i_revenant_tracker]
ID=i_memory 
NAME=Revenant Tracker
TYPE=t_eq_script

[ITEMDEF i_vengeful_spirit]
ID=i_memory 
NAME=Vengeful Spirit
TYPE=t_eq_script

ON=@Equip
timer=1

ON=@Timer
if (<<uid.<cont.tag0.victim>.flags> & statf_dead)
  finduid.<cont.tag.owner>.findid.i_revenant_tracker.remove
  finduid.<cont.tag.owner>.sysmessage Your revenant succeeded!
  cont.remove
  return 1
else
  cont.p=<uid.<cont.tag.victim>.p>
  cont.move <eval(rand(3))+-(rand(3))> 
  cont.attack <uid.<cont.tag.victim>>
  more1 -= 1
  if (<more1> < 1)
    finduid.<cont.tag.owner>.findid.i_revenant_tracker.remove
    finduid.<cont.tag.owner>.sysmessage Your revenant failed
    cont.remove
  endif
  timer=10
  return 1
endif

[CHARDEF c_revenant]
ID=c_man
NAME=Revenant
SOUND=snd_MONSTER_GHOST1
CAN=MT_USEHANDS|MT_EQUIP|MT_WALK|MT_RUN|MT_GHOST
DAM=30,40
TAG.PHYSICALDAMAGE=100
TAG.PHYSICALRESIST=65
TAG.FIRERESIST=20
TAG.COLDRESIST=65
TAG.POISONRESIST=100
TAG.ENERGYRESIST=65

ON=@Create
    EVENTS +e_revenant
    NPC=brain_monster
    COLOR=0455
    STR=200
    DEX=150
    INT=150
    SWORDSMANSHIP=100.0
    MAGICRESISTANCE=120.0
    TACTICS=100.0
    KARMA=-5000
    FAME=0

ON=@NPCRestock
    ITEMNEWBIE=i_death_shroud
    COLOR=0455
    ITEMNEWBIE=i_halberd
    COLOR=0455

[EVENTS e_revenant]
ON=@Death
finduid.<src.tag.owner>.findid.i_revenant_tracker.remove
finduid.<src.tag.owner>.sysmessage Your revenant failed

ON=@Kill
if (<argo.uid>==<src.tag.victim>)
  finduid.<src.tag.owner>.findid.i_revenant_tracker.remove
  finduid.<src.tag.owner>.sysmessage Your revenant succeeded!
  remove
endif

//#################################################\\
//#                 Vampiric Embrace              #\\
//#################################################\\

[itemdef i_vampire_form]
NAME=Vampire Form
ID=i_memory 
TYPE=t_eq_script
LAYER=layer_special

ON=@Equip
  src.body=c_vampire_bat
  src.color=0
  src.events +e_vampire_form

ON=@UnEquip
  src.body=<src.obody>
  src.color=<src.oskin>
  src.events -e_vampire_form

ON=@Timer
cont.stam += {5 10}
if (<cont.stam> > <cont.maxstam>)
  cont.stam=<cont.maxstam>
endif
cont.mana += {5 10}
if (<cont.mana> > <cont.maxmana>)
  cont.mana=<cont.maxmana>
endif
timer=10
return 1

[events e_vampire_form]
ON=@Spellcast
if (<argn>==56)
  src.sysmessage You can't do that in your current state
  return 1
endif

ON=@Hit
  hits += (<argn1>/100)*20
  if <hits> > <maxhits>
     hits=<maxhits>
  endif

[EOF]