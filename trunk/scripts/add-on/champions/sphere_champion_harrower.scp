[FUNCTION F_EXPLODECORPSE]
REF1=<UID>
FORCLIENTS 5
DAMAGE 33 <DEF.DAM_GOD> <ARGN1>
SPELLEFFECT S_POISON,100.0,<ARGN1>
ENDFOR

[FUNCTION F_EXPLODECORPSE_DROPALL]
LOCAL.OLDNEW=<NEW>
SERV.NEWITEM i_flesh_head
NEW.P=<P>
NEW.ATTR=ATTR_DECAY
NEW.UPDATE
SERV.NEWITEM i_flesh_torso
NEW.P=<P>
NEW.ATTR=ATTR_DECAY
NEW.MOVE S
NEW.UPDATE
SERV.NEWITEM i_flesh_leg
NEW.P=<P>
NEW.ATTR=ATTR_DECAY
NEW.MOVE E
NEW.UPDATE
SERV.NEWITEM i_flesh_leg
NEW.P=<P>
NEW.ATTR=ATTR_DECAY
NEW.MOVE N
NEW.UPDATE
REMOVE
FORCONT <UID> 0
DROP
ENDFOR
NEW=<LOCAL.OLDNEW>

//////////////////////////////

[CHARDEF 092]
DEFNAME=c_Harrower
NAME=Harrower
CAN=MT_WALK|MT_USEHANDS
DAM=141,175
SOUND=1150
ARMOR=65
MOVERATE=70
CATEGORY=Addon
SUBSECTION=Champs
DESCRIPTION=Harrower
TAG.IAMACHAMPION=1

ON=@Create
TAG.LIFETIER=2
NPC=brain_monster
KARMA=-10000
FAME=10000
STR={901 1000}
DEX={126 140}
INT={1000 1200}
MAXHITS=19500
MAXMANA=6000
WRESTLING=95.0
TACTICS=100.0
MAGICRESISTANCE=140.0
MAGERY=130.0
EVALUATINGINTEL=130.0
ITEMNEWBIE=i_spellbook
ADDCIRCLE 7,1

ON=@Death
TAG0.LIFETIER -= 1
IF (<TAG0.LIFETIER>)
HITS=<MAXHITS>
ACTION -1
RETURN 1
ENDIF

ON=@NPCLookAtChar
IF (<SRC.ISPLAYER>)
	IF !(<R20>)
	SRC.GO <P>
	SAY @,,2 1049503
	ENDIF
ENDIF
IF (<ISNEARTYPE T_CORPSE 10>)
	IF (<R20> == 1)
	REF1=<UID>
		FORITEMS 10
			IF (<BASEID> == i_corpse) && (!(<LOCAL.JAEXPLODIU>))
			F_EXPLODECORPSE <REF1.UID>
			F_EXPLODECORPSE_DROPALL
			LOCAL.JAEXPLODIU=1
			ENDIF
		ENDFOR
	ENDIF
ENDIF

ON=@DeathCorpse
LOCAL.NEW=<NEW>
SERV.NEWNPC C_TRUE_Harrower
NEW.GO <P>
NEW.SAY @,,2 1049499
REF1=<NEW>
FOR 12
SERV.NEWNPC C_TENTACLES_Harrower
NEW.GO <P>
NEW.FACE <REF1.UID>
NEW.MOVE <HVAL {-6 6}> <HVAL {-6 6}>
ENDFOR
NEW=<LOCAL.NEW>
ARGO.REMOVE

ON=@Hit
IF (!(<R20>))
	DORAND 4
	SAY @,,2 1049500
	SAY @,,2 1049501
	SAY @,,2 1049505
	SAY @,,2 1049507
	ENDDO
ENDIF

ON=@SpellEffect
SAY @,,2 1049502

ON=@GetHit
IF (!(<R20>))
	DORAND 2
	SAY @,,2 1049508
	SAY @,,2 1049504
	ENDDO
ENDIF
IF !(<R30>)
SRC.GO <P>
SAY @,,2 1049503
ENDIF
IF (<SRC.FLAGS>&statf_conjured)
	IF (!(<R50>))
	SRC.SPELLEFFECT S_DISPEL,1000,<UID>
	ENDIF
ENDIF

[CHARDEF C_TENTACLES_Harrower]
ID=C_CORPSER
NAME=Tentacles of Harrower
CAN=MT_NONMOVER|MT_USEHANDS
RANGE=9
DAM=57,93
ARMOR=60
CATEGORY=Addon
SUBSECTION=Champs
DESCRIPTION=Tentacles of the Harrower
TAG.LIFELEECH=50

ON=@Create
TAG.OVERRIDE.REGENVAL_1=25
COLOR=COLORS_BLACK
NPC=brain_monster
NAME=Tentacles of the Harrower
KARMA=-9000
FAME=5000
STR={300 400}
DEX={110 130}
INT={1000 1200}
MAXHITS=860
MAXMANA=1000
WRESTLING=90.0
TACTICS=90.0
MAGICRESISTANCE={120.0 140.0}
ANATOMY=100.0

ON=@NPCREstock
ITEM=i_gold,{900 1300}

[CHARDEF C_TRUE_Harrower]
ID=c_bog_thing
DEFNAME=C_TRUE_Harrower
CAN=MT_NONMOVER|MT_USEHANDS
NAME=True Harrower
DAM=141,175
SOUND=1150
ARMOR=80
CATEGORY=Addon
SUBSECTION=Champs
DESCRIPTION=True Harrower
TAG.IAMACHAMPION=1
TAG.LIFELEECH=50

ON=@Create
TAG.LIFETIER=2
NPC=brain_monster
TAG.OVERRIDE.REGENVAL_1=40
COLOR=038b
KARMA=-10000
FAME=10000
STR={900 1000}
DEX={110 130}
INT={1000 1200}
MAXHITS=19500
MAXMANA=10000
WRESTLING=110.0
TACTICS=110.0
MAGICRESISTANCE=160.0
MAGERY=140.0
ANATOMY=80.0
EVALUATINGINTEL=140.0
ITEMNEWBIE=i_spellbook
ADDCIRCLE 7,1

ON=@NPCLookAtChar
IF (<SRC.ISPLAYER>)
	IF !(<R30>)
	SRC.GO <P>
	ATTACK <SRC>
	ENDIF
ENDIF
IF (<ISNEARTYPE T_CORPSE 10>)
	IF (<R20> == 1)
	REF1=<UID>
		FORITEMS 10
			IF (<BASEID> == i_corpse) && (!(<LOCAL.JAEXPLODIU>))
			F_EXPLODECORPSE <REF1.UID>
			F_EXPLODECORPSE_DROPALL
			LOCAL.JAEXPLODIU=1
			ENDIF
		ENDFOR
	ENDIF
ENDIF

ON=@GetHit
IF (<SRC.ISPLAYER>)
	IF !(<R35>)
	SRC.GO <P>
	ATTACK <SRC>
	ENDIF
ENDIF
IF (<SRC.FLAGS>&statf_conjured)
	IF (!(<R50>))
	SRC.SPELLEFFECT S_DISPEL,1000,<UID>
	ENDIF
ENDIF

ON=@Hit
SRC.SPELLEFFECT S_POISON,100.0,<UID>

ON=@Death
TAG0.LIFETIER -= 1
IF (<TAG0.LIFETIER>)
HITS=<MAXHITS>
ACTION -1
RETURN 1
ENDIF

LOCAL.NEW=<NEW>
VAR.HARROWER=
FOR 0 5
	DOSWITCH <dLOCAL._FOR>
	REF1=<VAR0.HARROWER.SKULL.Greed>
	REF1=<VAR0.HARROWER.SKULL.Pain>
	REF1=<VAR0.HARROWER.SKULL.Venom>
	REF1=<VAR0.HARROWER.SKULL.Power>
	REF1=<VAR0.HARROWER.SKULL.Death>
	REF1=<VAR0.HARROWER.SKULL.Enlightenment>
	ENDDO
	IF (0<REF1>)
	REF1.REMOVE
	ENDIF
ENDFOR
VAR.HARROWER.SKULL.Greed=
VAR.HARROWER.SKULL.Pain=
VAR.HARROWER.SKULL.Venom=
VAR.HARROWER.SKULL.Power=
VAR.HARROWER.SKULL.Death=
VAR.HARROWER.SKULL.Enlightenment=
VAR.HARROWER.TOTALSKULLS=
SERV.B @0ad The Harrower has been slain! Congratulations!
IF (<ATTACKER>)
	FOR 0 <EVAL <ATTACKER>-1>
		IF (<ATTACKER.<dLOCAL._FOR>.DAM>)
		REF2=<ATTACKER.<dLOCAL._FOR>>
			IF (!(0<LIST.ChampionPowerScroll.FINDELEM <REF2>>))
			LIST.ChampionPowerScroll.ADD=<REF2>
			ENDIF
		ENDIF
	ENDFOR
	IF (0<LIST.ChampionPowerScroll.COUNT>)
	LOCAL.TOTALPOWERSCROLLSTOBEGIVEN=<LIST.ChampionPowerScroll.COUNT>
		IF (<LOCAL.TOTALPOWERSCROLLSTOBEGIVEN> > 16)
		LOCAL.TOTALPOWERSCROLLSTOBEGIVEN=16
		ENDIF
		WHILE (0<LIST.ReceivedPowerScroll.COUNT> < <LOCAL.TOTALPOWERSCROLLSTOBEGIVEN>)
			REF2=<LIST.ChampionPowerScroll.<R<LIST.ChampionPowerScroll.COUNT>>>
			IF (!(0<LIST.ReceivedPowerScroll.FINDELEM <REF2>>))
			SERV.NEWITEM=i_stat_power_scroll
			REF2.BOUNCE <NEW>
			REF2.TRIGGER @ReceiveStatPowerScroll
				FOR x 1 5
				SERV.NEWITEM=<HVAL <DEF.CHAMP_LOOT_VERY_HIGH>>
				REF2.BOUNCE <NEW>
				ENDFOR
				IF (<DEF.scp.Champions_ArtyChamp> >= <R1,100>)
				SERV.NEWITEM <DEF.champ_Harrower_REWARD>
				REF2.BOUNCE <NEW>
				ENDIF
				REF2.HARROWERPOINTS += 1
				LIST.ReceivedPowerScroll.ADD <REF2>
			ENDIF
		ENDWHILE
	ENDIF	
ENDIF
LIST.ChampionPowerScroll.CLEAR
LIST.ReceivedPowerScroll.CLEAR
NEW=<LOCAL.NEW>

[TYPEDEF T_Harrower_OFFER]
ON=@DClick
IF (<DISTANCE> > 3)
SRC.MESSAGE @,,2 500446
ELIF (<VAR0.HARROWER>)
SRC.MESSAGE @,3,1 The harrower has already been spawned.
ELIF (<VAR0.HARROWER.SKULL.<TAG0.SKULLNAME>>)
MESSAGE @,,2 1049487
ELSE
TARGET @,,2 1049485
ENDIF
RETURN 1

ON=@Targon_Item
IF (<ARGO.BASEID> != i_champion_skull)
MESSAGE @,,2 1049488
ELIF (<VAR0.HARROWER>)
SRC.MESSAGE @,3,1 The harrower has already been spawned.
ELIF (<VAR0.HARROWER.SKULL.<TAG0.SKULLNAME>>)
MESSAGE @,,2 1049487
ELIF (<ARGO.CONT> != <SRC.FINDLAYER.<DEF.layer_pack>.UID>)
SRC.SYSMESSAGE @,,2 1049486
ELIF (!(STRMATCH(*<TAG0.SKULLNAME>*,<ARGO.NAME>)))
MESSAGE @,,2 1049488
ELSE
TRY VAR.HARROWER.SKULL.<TAG0.SKULLNAME>=<ARGO.UID>
VAR0.HARROWER.TOTALSKULLS += 1
EFFECT 2,i_fx_sparkle,16,20
ARGO.P=<P>
ARGO.ATTR=<DEF.ATTR_MOVE_NEVER>|<DEF.ATTR_CURSED>
ARGO.UPDATE
	IF (<VAR0.HARROWER.TOTALSKULLS> == 6)
	SERV.NEWNPC C_Harrower
		DORAND 7
		NEW.P=5579,1858
		NEW.P=5267,669
		NEW.P=5583,895,30
		NEW.P=5250,776
		NEW.P=6103,33,27
		NEW.P=5477,189
		NEW.P=5692,536
		ENDDO
	NEW.FIX
	REF1=<NEW>
	SERV.NEWITEM i_moongate_blue
	NEW.P=5138,1767
	NEW.TYPE=T_Harrower_GATE
	NEW.TAG.LOCAL=<STRARG <REF1.REGION.NAME>>
	NEW.COLOR=038b
	NEW.TIMER <EVAL 60*10>
	NEW.ATTR=ATTR_MOVE_NEVER|ATTR_DECAY|ATTR_CAN_DECAY
	ENDIF
ENDIF
RETURN 1

ON=@Targon_Char
MESSAGE @,,2 1049488
RETURN 1

[TYPEDEF T_Harrower_GATE]
ON=@Step
SRC.GO <TAG0.LOCAL>

[EOF]