//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED:
VERSION=0.56b
//
// SCRIPT CREDITS
// --------------
// Cloud_BR - All the script
//
// SCRIPT DOCUMENTATION
// --------------------
// OSI-Like SpiritSpeak

[SKILL 32]
DEFNAME=SKILL_SPIRITSPEAK
KEY=SpiritSpeak
TITLE=Medium
PROMPT_MSG=
DELAY=2.0
STAT_STR=20
STAT_INT=70
STAT_DEX=30
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,200.0

on=@wait
if <def.scp.NewSkills_SpiritSpeak> == 1
f_start_spiritspeak
return 1
endif

on=@select
f_skill_delay <getargv 0 <def.skill.32.waittime>>

on=@success
f_skill_delay <getargv 1 <def.skill.32.waittime>>

ON=@Fail
SRC.MESSAGE @,,2 502443
f_skill_delay <getargv 2 <def.skill.32.waittime>>

ON=@Abort
	SRC.SYSMESSAGE You stop trying to commune with the netherworld.

[FUNCTION f_start_spiritspeak]
if <skillusequick spiritspeak <eval <r1000> - <spiritspeak>>>
f_skill_delay <getargv 1 <def.skill.32.waittime>>
skillgain spiritspeak 30
	if <hits> >= <maxhits>
	src.sysmessage @03c1,3,2 502444
	endif
	foritems 3
	if <type>==t_corpse
		if (<canseelos>)
			if !<morey>
			local.maxdamhealed=<eval <src.spiritspeak>/20>
			local.mindamhealed=<eval (<src.fame>/500)+1>
				if <local.mindamhealed> < <local.maxdamhealed>
				src.hits += <r<local.mindamhealed>,<eval <local.maxdamhealed>+1>>
				else
				src.hits += <local.maxdamhealed>
				endif
			src.sound 586
			src.say @03b2,3,2 1062074
			src.anim 17
			morey=1
			color=0349
				if <src.hits> > <src.maxhits>
				src.hits=<src.maxhits>
				endif
			src.sysmessage @03c1,3,2 1061287
			src.effect 3,i_fx_bless_effect,1,20
			return 1
			endif
		endif
	endif
	endfor
	if <src.mana> <= 10
	src.sysmessage @03c1,3,2 1061285
	return 1
	else
	local.maxdamhealed=<eval (<spiritspeak>/100)+4>
	local.mindamhealed=<eval (<local.maxdamhealed>/2)+1>
	src.hits += <r<local.mindamhealed>,<eval <local.maxdamhealed>+1>>
		if <src.hits> > <src.maxhits>
		src.hits=<src.maxhits>
		endif
	src.sysmessage @03c1,3,2 1061286
	src.mana -= 10
	src.effect 3,i_fx_bless_effect,1,20
	endif
else
sysmessage @,,2 502443
f_skill_delay <getargv 2 <def.skill.32.waittime>>
endif
return 1

[eof]