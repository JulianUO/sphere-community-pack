//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED:
VERSION=0.56b


[COMMENT sphere_skill_blacksmithy_functions]
"SCRIPT CREDITS
 --------------
 Coruja - Base script
 ShiryuX - Updated to the pack

 SCRIPT DOCUMENTATION
 --------------------
 Functions used for Blacksmithing Menu"


[SKILL 7]
DEFNAME=SKILL_Blacksmith
KEY=Blacksmithing
TITLE=Blacksmith
PROMPT_MSG=
DELAY=1.0
STAT_STR=95
STAT_INT=15
STAT_DEX=25
BONUS_STR=75
BONUS_DEX=20
BONUS_INT=5
BONUS_STATS=10
ADV_RATE=2.5,50.0,200.0
VALUES=1,20,100

on=@fail
src.sysmessage @,,2 <def.scp.craft_msg_fail>

ON=@Abort
src.sysmessage @,,2 <def.scp.craft_msg_fail>


[FUNCTION f_craft_blacksmith_checkforge]
IF (!<ISNEARTYPE t_forge 3>) || (!<ISNEARTYPE t_anvil 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithingneeds>
   RETURN 1
ENDIF
RETURN 0

[FUNCTION f_craft_blacksmith_exceptionalbonus]
REF5=<ARGS>
NAME=Exceptional <NAME>
IF (<ISWEAPON>)
   tag0.damageincrease += <eval 35 + (<ref5.armslore> / 200)>
ELIF (<ISARMOR>)
   modar += <eval (<armor.high> * 15) / 100>
ENDIF


[FUNCTION f_craft_blacksmith_repair]
IF (!<ISNEARTYPE t_forge 3>) || (!<ISNEARTYPE t_anvil 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithrepairneeds>
   f_resenddialog d_craft_menu
   RETURN 1
ENDIF
TARGETF f_craft_blacksmith_repair_targ
SYSMESSAGE <DEF.SCP.craft_repair_targ>

[FUNCTION f_craft_blacksmith_repair_targ]
IF (!<ISNEARTYPE t_forge 3>) || (!<ISNEARTYPE t_anvil 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithrepairneeds>
   LOCAL.FailedRequirement=1
ELIF (!<ARGO.ISTEVENT.e_equipitem>)
   CTAG.Craft.Message=<DEF.SCP.craft_repair_notitem>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.SKILLMAKE.1.KEY>!=<SRC.CTAG0.craft.skill>)
   CTAG.Craft.Message=<DEF.SCP.craft_repair_wrongskill>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.CONT> != <FINDLAYER(layer_pack).UID>)
   CTAG.Craft.Message=<DEF.SCP.craft_repair_inpack>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.SKILLMAKE.1.KEY> != skill_blacksmith) || !((<ARGO.TYPE> == t_armor) || (<ARGO.TYPE> == t_shield) || (<ARGO.ISWEAPON>))
   CTAG.Craft.Message=<DEF.SCP.craft_repair_wrongskill>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.MORE1L> >= <ARGO.MORE1H>)
   CTAG.Craft.Message=<DEF.SCP.craft_repair_fullrepair>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.MORE1H> <= 0)
   CTAG.Craft.Message=<DEF.SCP.craft_repair_cannot>
   LOCAL.FailedRequirement=1
ENDIF
IF (<LOCAL.FailedRequirement>)
   f_resenddialog d_craft_menu
   RETURN 1
ENDIF
f_loopanim 11,3,1
f_loopsound 42,3,1
LOCAL.difficulty=<EVAL ((((<ARGO.MORE1H> - <ARGO.MORE1L>) * 1250) / <ARGO.MORE1H>) - 250) / 10>
LOCAL.decrease=<EVAL 30 - (<Blacksmithing> / 33)>
IF (<LOCAL.decrease> < 0)
   LOCAL.decrease=0
   ENDIF
LOCAL.decrease += <EVAL {2 4}>
ARGO.MORE1H=<EVAL (<ARGO.MORE1H> * (100 - <LOCAL.decrease>)) / 100>
IF (<LOCAL.difficulty> > <R100>)
   CTAG.craft.message=<DEF.SCP.craft_repair_fail>
   RETURN 1
ELSE
   ARGO.MORE1L=<ARGO.MORE1H>
   CTAG.craft.message=<DEF.SCP.craft_repair_success>
ENDIF
RESENDTOOLTIP


[FUNCTION f_craft_blacksmith_smelt]
IF (!<ISNEARTYPE t_forge 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithsmeltneeds>
   f_resenddialog d_craft_menu
   RETURN 1
ENDIF
TARGETF f_craft_blacksmith_smelt_targ
SYSMESSAGE <DEF.SCP.craft_smelt_targ>

[FUNCTION f_craft_blacksmith_smelt_targ]
IF (!<ISNEARTYPE t_forge 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithsmeltneeds>
   RETURN 1
ELIF !<ARGO.ISITEM>
   SYSMESSAGE <DEF.SCP.craft_smelt_notitem>
   RETURN 1
ELIF (<ARGO.TYPE>==t_clothing) || (<ARGO.ATTR>&attr_magic) || !((<ARGO.ISARMOR>) || (<ARGO.ISWEAPON>) || STRMATCH("*ingot*","<ARGO.RESOURCES.1>"))
   SYSMESSAGE <DEF.SCP.craft_smelt_notsmeltable>
   RETURN 1
ELSE
	IF !<ARGO.QUALITY>
      LOCAL.AMOUNT=1
	ELSE
      LOCAL.AMOUNT=<MULDIV <STRARG <ARGO.RESOURCES.1>>,60,100>
	ENDIF
   SERV.NEWITEM=<STREAT <ARGO.RESOURCES.1>>,<dLOCAL.AMOUNT>
   NEW.CONT=<ARGO.CONT>
   ARGO.REMOVE
   SYSMESSAGE <DEF.SCP.craft_smelt_success>
ENDIF


[FUNCTION f_craft_blacksmith_enhance]
LOCAL.DefnameValue=<SRC.CTAG0.craft.skill>_material_<SRC.dCTAG.craft.material.<SRC.CTAG.craft.skill>>

IF (!<ISNEARTYPE t_forge 3>) || (!<ISNEARTYPE t_anvil 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithenhanceneeds>
   LOCAL.FailedRequirement=1
ELIF !(<FINDLAYER(layer_hand1).ISTEVENT.t_runic_smith_hammer>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notrunicB>
   LOCAL.FailedRequirement=1
ELIF (STRMATCH(<DEF.<LOCAL.DefnameValue>_resourceid>,))
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notspecial>
   LOCAL.FailedRequirement=1
ELIF !(<SRC.RESTEST 5 <DEF.<LOCAL.DefnameValue>_resourceid>>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_nomaterial>
   LOCAL.FailedRequirement=1
ENDIF
IF (<LOCAL.FailedRequirement>)
   f_resenddialog d_craft_menu
   RETURN 1
ENDIF
TARGETF f_craft_blacksmith_enhance_targ
SYSMESSAGE <DEF.SCP.craft_enhance_targ>

[FUNCTION f_craft_blacksmith_enhance_targ]
LOCAL.DefnameValue=<SRC.CTAG0.craft.skill>_material_<SRC.dCTAG.craft.material.<SRC.CTAG.craft.skill>>

IF (!<ISNEARTYPE t_forge 3>) || (!<ISNEARTYPE t_anvil 3>)
   CTAG.craft.message=<DEF.SCP.craft_msg_smithenhanceneeds>
   LOCAL.FailedRequirement=1
ELIF !(<FINDLAYER(layer_hand1).ISTEVENT.t_runic_smith_hammer>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notrunicB>
   LOCAL.FailedRequirement=1
ELIF (STRMATCH(<DEF.<LOCAL.DefnameValue>_resourceid>,))
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notspecial>
   LOCAL.FailedRequirement=1
ELIF !(<SRC.RESTEST 5 <DEF.<LOCAL.DefnameValue>_resourceid>>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_nomaterial>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.f_craft_blacksmith_alreadyEnhanced>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_already>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.ATTR>&attr_magic)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_enchanted>
   LOCAL.FailedRequirement=1
ELIF !(<ARGO.ISTEVENT.e_equipitem>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notitem>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.SKILLMAKE.1.KEY>!=<SRC.CTAG0.craft.skill>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_notitem>
   LOCAL.FailedRequirement=1
ELIF (<ARGO.CONT> != <FINDLAYER(layer_pack).UID>)
   CTAG.Craft.Message=<DEF.SCP.craft_enhance_inpack>
   LOCAL.FailedRequirement=1
ENDIF
IF (<LOCAL.FailedRequirement>)
   f_resenddialog d_craft_menu
   RETURN 1
ENDIF
SRC.CONSUME 5 <DEF.<LOCAL.DefnameValue>_resourceid>
f_loopanim 11,3,1
f_loopsound 42,3,1
LOCAL.materialPenalty=<EVAL <STREAT <DEF.<LOCAL.DefnameValue>_skill>>>
LOCAL.difficulty=<EVAL <Blacksmithing>-<ARGO.SKILLMAKE.1.VALUE>-<LOCAL.materialPenalty>>
LOCAL.chance=<R1000>

IF (<LOCAL.difficulty> < 0)
   SYSMESSAGE <DEF.SCP.craft_enhance_totalfail>
   ARGO.CONSUME
   RETURN 1
ENDIF
IF (<LOCAL.Chance> > <LOCAL.difficulty>)
   SYSMESSAGE <DEF.SCP.craft_enhance_fail>
   RETURN 1
ELSE
   ARGO.f_craft_blacksmith_enhance_item
   SYSMESSAGE <DEF.SCP.craft_enhance_success>
ENDIF

[FUNCTION f_craft_blacksmith_alreadyEnhanced]
LOCAL.BadResources="i_ingot_copper,i_ingot_gold,i_ingot_rose,i_ingot_agapite,i_ingot_tyrian,i_ingot_silver,i_ingot_shadow,i_ingot_byzantine,i_ingot_magmite,i_ingot_verite,i_ingot_bloodrock,i_ingot_blackrock,i_ingot_valorite,i_ingot_lavarock,i_ingot_royal,i_ingot_mytheril,i_ingot_titanium,i_ingot_zanite"
IF (<TAG0.Enhanced>)
   RETURN 1
ENDIF
ARGS=<SERV.ITEMDEF.<DEFNAME>.RESOURCES>
FOR 1 <ARGV>
   IF (STRMATCH(*<SERV.ITEMDEF.<DEFNAME>.RESOURCES.<LOCAL._FOR>.KEY>*,<LOCAL.BadResources>))
      RETURN 1
   ENDIF
ENDFOR
RETURN 0

[FUNCTION f_craft_blacksmith_enhance_item]
LOCAL.DefnameValue=<SRC.CTAG0.craft.skill>_material_<SRC.dCTAG.craft.material.<SRC.CTAG.craft.skill>>
LOCAL.Material=<DEF.<LOCAL.DefnameValue>_resourceid>

TAG.Enhanced=<LOCAL.Material>
IF (<TAG0.REQSTR>)
   TAG0.REQSTR += <EVAL <DEF.<LOCAL.DefnameValue>_modreqstr>>
ENDIF
IF (<HITPOINTS>)
   IF (<DEF.<LOCAL.DefnameValue>_durability>==3)
      LOCAL.DurabilityIncrease=<R24,36>
      MORE1L += <LOCAL.DurabilityIncrease>
      MORE1H += <LOCAL.DurabilityIncrease>
   ELSEIF (<DEF.<LOCAL.DefnameValue>_durability>==4)
      LOCAL.DurabilityIncrease=<R24,36>
      MORE1L += <LOCAL.DurabilityIncrease>
      MORE1H += <LOCAL.DurabilityIncrease>
   ENDIF
ENDIF
NAME=<DEF.<LOCAL.DefnameValue>> <NAME>
COLOR=<DEF.<LOCAL.DefnameValue>_color>
ATTR |= attr_identified

IF (<ISWEAPON>)
   TAG0.PhysicalDamagePercent += <EVAL <getargv 0 <DEF.<LOCAL.DefnameValue>_moddamage>>>
   TAG0.FireDamagePercent += <EVAL <getargv 1 <DEF.<LOCAL.DefnameValue>_moddamage>>>
   TAG0.ColdDamagePercent += <EVAL <getargv 2 <DEF.<LOCAL.DefnameValue>_moddamage>>>
   TAG0.PoisonDamagePercent += <EVAL <getargv 3 <DEF.<LOCAL.DefnameValue>_moddamage>>>
   TAG0.EnergyDamagePercent += <EVAL <getargv 4 <DEF.<LOCAL.DefnameValue>_moddamage>>>
ELSEIF (<ISARMOR>)
   TAG0.ResPhysical += <EVAL <getargv 0 <DEF.<LOCAL.DefnameValue>_modresist>>>
   TAG0.ResFire += <EVAL <getargv 1 <DEF.<LOCAL.DefnameValue>_modresist>>>
   TAG0.ResCold += <EVAL <getargv 2 <DEF.<LOCAL.DefnameValue>_modresist>>>
   TAG0.ResPoison += <EVAL <getargv 3 <DEF.<LOCAL.DefnameValue>_modresist>>>
   TAG0.ResEnergy += <EVAL <getargv 4 <DEF.<LOCAL.DefnameValue>_modresist>>>
ENDIF

[EOF]
