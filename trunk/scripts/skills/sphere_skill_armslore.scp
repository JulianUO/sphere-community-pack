//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED: 
VERSION=0.56b

[SKILL 4]
DEFNAME=SKILL_ARMSLORE
KEY=ArmsLore
TITLE=Arms%s
PROMPT_MSG=@,,2 500906
DELAY=0.0
STAT_STR=10
STAT_INT=5
STAT_DEX=15
BONUS_STR=33
BONUS_DEX=33
BONUS_INT=34
BONUS_STATS=25
ADV_RATE=2.5,50.0,200.0

on=@select
f_skill_delay <getargv 0,<def.skill.4.waittime>>

on=@success
f_skill_delay <getargv 1,<def.skill.4.waittime>>
if <def.scp.NewSkills_ArmsLore> == 1
local.percent = <eval (<act.more1l>*100)/<act.more1h>>
local.dcliloc = <eval (<local.percent>/10)+<def.armslore.durability.start>>
sysmessage @,,2 <local.dcliloc>
	if <act.isweapon>
	local.wcliloc = <def.armslore.weapon.start>
	local.weapdam = <eval (<act.dam.lo>+<act.dam.hi>)/2>
	local.wcliloc += <eval ((<local.weapdam>/5)+1)*9>
		if <act.layer> == 2
			if (<act.type> == t_weapon_bow) || (<act.type> == t_weapon_xbow)
			local.wcliloc += 9
			else
			local.wcliloc += 2
			endif
		else
		local.wcliloc ++
		endif
	sysmessage @,,2 <local.wcliloc>
	elif <act.isarmor>
	local.armor = <eval ((<act.armor.high>+<act.tag0.modar>)*<def.armor_<act.layer>>)/100>
	local.acliloc = <eval ((<local.armor>/5)+1)+<def.armslore.armor.start>>
	sysmessage @,,2 <local.acliloc>
	endif
skillgain ArmsLore <eval <ArmsLore>/10>
return 1
endif

ON=@Fail
f_skill_delay <getargv 2,<def.skill.4.waittime>>
sysmessage @,,2 500353 // You are not certain...

[defname arms_lore_cliloc]
armslore.durability.start	1038284
armslore.weapon.start		1038215
armslore.armor.start		1038295

[eof]