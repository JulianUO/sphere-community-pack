//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED:
VERSION=0.56b

[COMMENT comment_necromancy_spells_extra]

SCRIPT CREDITS
--------------
Wiccanian, ShiryuX, Anarch, Others...

SCRIPT DOCUMENTATION
--------------------
This file contains items, events, and functions required by the necromancy
spell definitions.

Regarding the 4 transformation spells (Horrific Beast, Lich Form, Wraith
Form, and Vampiric Embrace.)  They all use the i_mem_transform item
to equip the necessary effect (which are primarily implemented using the
item equip event t_iprop)

//////////////////////
// Horrific Beast   //
// Lich Form        //
// Wraith Form      //
// Vampiric Embrace //
//////////////////////

[ITEMDEF i_mem_transform]
//Duration is permanent til death or another transformation spell is cast.
ID=i_trophy_trollhead
NAME=transformation memory
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=0
TEVENTS=t_iprop
ON=@Create
   ATTR=attr_decay|attr_can_decay
   COLOR=04d
ON=@Equip
   ATTR=attr_decay|attr_can_decay
   TAG.PlayerColor=<SRC.COLOR>
   SRC.COLOR=0
   SRC.BODY=<TAG.TransformCreature>
   SRC.SYSMESSAGELOC color_text,1075823,<TAG.TransformArticle>,<SERV.CHARDEF.<TAG.TransformCreature>.NAME>//"You are transformed into ~1 ~2."
ON=@UnEquip
   SRC.SYSMESSAGELOC color_text,1071176//"You can't maintain your special form anymore."
   SRC.BODY=<SRC.OBODY>
   SRC.COLOR=<TAG.PlayerColor>


//////////////////
// Animate Dead //
//////////////////

[FUNCTION f_AnimateDeadTarg]
IF (<ARGO.TYPE>==t_corpse)
   IF (<ARGO.LINK.UID>)
      SYSMESSAGe @,,2 1061085 //"There's not enough life force there to animate."
      RETURN 0
   ENDIF
   local.castability = <eval (((<necromancy>*3)+(<spiritspeak>*7))/10)*18>
   local.delay = <eval (<necromancy> / 10) + <spiritspeak>>
   if <local.castability> > <argo.tag0.corpsefame>
      if <serv.chardef.<argo.morex>.tag0.animateid>
         serv.newnpc <serv.chardef.<argo.morex>.tag0.animateid>
      else
         serv.newnpc <f_AnimateCharacter> <local.castability>
      endif
      new.p = <argo.p>
      new.flags ^= statf_conjured
      new.maxhits = <eval <local.castability>/50>
      new.hits = <new.maxhits>
      new.f_MakePetOf <uid>
      ref10 = <new.uid>
      serv.newitem i_rune_summon_creature
      new.timer = <local.delay>
      ref10.equip <new.uid>
   endif
else
   sysmessage @,,2 1061084 // You cannot animate that.
   return 0
endif

[FUNCTION f_AnimateCharacter]
IF (<ARGS> > 17500)
   RETURN c_skeletal_dragon
ELSEIF (<ARGS> > 15000)
   RETURN c_lich_lord
ELSEIF (<ARGS> > 12500)
   RETURN c_flesh_golem
ELSEIF (<ARGS> > 10000)
   RETURN c_mummy
ELSEIF (<ARGS> > 8000)
   RETURN c_lich
ELSEIF (<ARGS> > 6000)
   RETURN c_skeleton_knight
ELSEIF (<ARGS> > 4000)
   RETURN c_skeleton_mage
ELSEIF (<ARGS> > 2000)
   RETURN c_patchwork_skeleton
ELSE
   RETURN c_mound_maggots
ENDIF

[FUNCTION f_AnimateDeadDispel]
UID.<ARGS>.HITS=0


////////////////
// Blood Oath //
////////////////

[FUNCTION f_BloodOathDispel]
IF (<TAG0.BloodOathCaster>)
   UID.<tag0.BloodOathCaster.tag.BloodOath
   UID.<tag0.BloodOathCaster>.events -e_BloodOathCurse
   TAG.BloodOathCaster
ENDIF

[EVENTS e_BloodOath]
ON=@Hit
   UID.<TAG0.BloodOath>.DAMAGE <EVAL (<ARGN1>*20)/100> <ARGN2> <UID>
ON=@SpellCast
   UID.<TAG0.BloodOath>.SPELLEFFECT <ARGN> <ARGN2>
ON=@SpellEffect
   IF (<ARGN> == 34) || (<ARGN> == 41)
      f_BloodOathDispel
   ENDIF


/////////////////
// Corpse Skin //
/////////////////

[ITEMDEF i_rune_corpse_skin]
ID=i_memory
NAME=Corpse Skin Effect
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_armor
TAG.ResPhysical=10
TAG.ResCold=10
TAG.ResFire=-10
TAG.ResPoison=-10
ON=@Equip
   SRC.COLOR=059f
   SRC.EVENTS +e_CorpseSkin
ON=@UnEquip
   CONT.COLOR=<CONT.OSKIN>
   CONT.EVENTS -e_CorpseSkin
ON=@Timer
   REMOVE
   RETURN 1

[EVENTS e_CorpseSkin]
ON=@SpellEffect
   IF (<ARGN> == 34) || (<ARGN> == 41)
      CONSUME i_rune_corpse_skin
   ENDIF


//////////////////
// Curse Weapon //
//////////////////

[EVENTS e_CurseWeapon]
ON=@Equip
   IF !<SRC.ISEVENT.e_CurseWeaponEffect>
      SRC.EVENTS +e_CurseWeaponEffect
   ENDIF
ON=@Unequip
   SRC.EVENTS -e_CurseWeaponEffect

[EVENTS e_CurseWeaponEffect]
ON=@Hit
   SOUND 1317
   HITS += <EVAL <ARGN1> / 2>
   IF (<HITS> > <MAXHITS>)
      HITS=<MAXHITS>
   ENDIF

[FUNCTION f_CurseWeaponDispel]
IF (<CONT.ISPLAYER>)
   CONT.EVENTS -e_CurseWeaponEffect
   EVENTS -e_CurseWeapon
ENDIF


////////////////
// Pain Spike //
////////////////

[FUNCTION f_PainSpikeDispel]
//SERV.LOG called this
TAG0.spell.painspike
IF !(<FLAGS>&statf_dead)
   HITS += <ARGS>
ENDIF


//////////////////
// Curse Weapon //
//////////////////

[ITEMDEF i_curse_weapon]
ID=i_memory
NAME=Curse Weapon
TYPE=t_eq_script
LAYER=layer_special
ON=@Equip
   SRC.EVENTS +e_curse_weapon
   LINK=<SRC.TAG.cursed_weapon>
ON=@UnEquip
   CONT.EVENTS -e_curse_weapon
   CONT.TAG.cursed_weapon=
   CONT.SOUND snd_spell_magic_arrow
ON=@Timer
   REMOVE
   RETURN 1

[EVENTS e_curse_weapon]
ON=@Hit
   IF (<ARGO.UID>==<TAG0.cursed_weapon>)
     SOUND 1317
     HITS += <ARGN1>/2
     IF (<HITS> > <MAXHITS>)
        HITS=<MAXHITS>
     ENDIF
   ENDIF


///////////////
// Evil Omen //
///////////////

[EVENTS e_evil_omen]
ON=@GetHit
   ARGN1 += <ARGN1>/4
   EVENTS -e_evil_omen
ON=@SpellEffect
   IF (<ARGN> == 34) || (<ARGN> == 41)
      EVENTS -e_evil_omen
   ENDIF


//////////////
// Mind Rot //
//////////////

[ITEMDEF i_mind_rot]
ID=i_memory
NAME=mind rot
TYPE=t_eq_script
ON=@Equip
   SRC.EVENTS +e_mind_rot
ON=@UnEquip
   CONT.EVENTS -e_mind_rot
ON=@Timer
   REMOVE
   RETURN 1

[EVENTS e_mind_rot]
ON=@SpellSelect
   MANA -= {5 15}
   IF (<SRC.MANA> < 0)
      SRC.MANA=0
   ENDIF
ON=@SpellEffect
   IF (<ARGN> == 34) || (<ARGN> == 41)
      CONSUME i_mind_rot
   ENDIF


/////////////////////
// Summon Familiar //
/////////////////////

[SKILLMENU sm_summon_familiar]
Which familiar do you wish to summon?

ON=i_pet_hordedaemon Horde Minion
TEST=necromancy 30.0 spiritspeak 30.0
SUMMON=c_familiar_horde_minion

ON=i_pet_wisp Shadow Wisp
TEST=necromancy 50.0 spiritspeak 50.0
SUMMON=c_familiar_wisp_shadow

ON=i_pet_wolf Dark Wolf
TEST=necromancy 60.0 spiritspeak 60.0
SUMMON=c_familiar_dark_wolf

ON=i_pet_snake_giant Death Adder
TEST=necromancy 80.0 spiritspeak 80.0
SUMMON=c_familiar_death_adder

ON=i_pet_vampirebat Vampire Bat
TEST=necromancy 100.0 spiritspeak 100.0
SUMMON=c_familiar_vampire_bat



///////////////////
// Poison Strike //
///////////////////

[ITEMDEF i_poison_cloud]
ID=i_fx_smoke
NAME=Poison Cloud
ON=@Create
   COLOR=0787
ON=@Step
   SRC.SPELLEFFECT 20 750
   SRC.DAMAGE <MORE1> dam_poison <UID>
   REMOVE
   RETURN 1


//////////////
// Strangle //
//////////////

[ITEMDEF i_strangle]
ID=i_memory
NAME=Strangle
TYPE=t_eq_script
LAYER=layer_special
ON=@Timer
   local.multiplier=<EVAL (3 - (<CONT.STAM> / <CONT.MAXSTAM>) * 2)>
   local.damage=<EVAL <MORE2> * <LOCAL.MULTIPLIER>>
   CONT.EFFECT 3,i_fx_heal_effect,16,20,0,01,1
   CONT.DAMAGE <local.damage> dam_poison <CONT.UID>
   MORE1 -= 1
   IF (<MORE1> < 1)
      REMOVE
   ENDIF
   TIMER=<MORE1>
   RETURN 1


/////////////////////
// Vengeful Spirit //
/////////////////////

[ITEMDEF i_revenant_tracker]
ID=i_memory
NAME=Revenant Tracker
TYPE=t_eq_script

[ITEMDEF i_vengeful_spirit]
ID=i_memory
NAME=Vengeful Spirit
TYPE=t_eq_script
ON=@Equip
   TIMER=1
ON=@Timer
   IF (<UID.<CONT.TAG0.Victim>.FLAGS>&statf_dead)
      UID.<CONT.TAG.Owner>.FINDID.i_revenant_tracker.REMOVE
      UID.<CONT.TAG.Owner>.SYSMESSAGE Your revenant succeeded!
      CONT.REMOVE
      RETURN 1
   ELSE
      CONT.P=<UID.<CONT.TAG.Victim>.P>
      CONT.MOVE <EVAL(RAND(3)) - (RAND(3))>
      CONT.ATTACK <UID.<CONT.TAG.Victim>>
      MORE1 -= 1
      IF (<MORE1> < 1)
         UID.<CONT.TAG.OWNER>.FINDID.i_revenant_tracker.REMOVE
         UID.<CONT.TAG.Owner>.SYSMESSAGE Your revenant failed
         CONT.REMOVE
      ENDIF
      TIMER=10
      RETURN 1
   ENDIF

[CHARDEF c_revenant]
ID=c_man
NAME=Revenant
SOUND=snd_MONSTER_GHOST1
CAN=MT_USEHANDS|MT_EQUIP|MT_WALK|MT_RUN|MT_GHOST
DAM=30,40
TAG.DamPhysical=100
TAG.PHYSICALRESIST=65
TAG.FIRERESIST=20
TAG.COLDRESIST=65
TAG.POISONRESIST=100
TAG.ENERGYRESIST=65
ON=@Create
   EVENTS +e_revenant
   NPC=brain_monster
   COLOR=0455
   STR=200
   DEX=150
   INT=150
   SWORDSMANSHIP=100.0
   MAGICRESISTANCE=120.0
   TACTICS=100.0
   KARMA=-5000
   FAME=0
ON=@NPCRestock
   ITEMNEWBIE=i_deathshroud
   COLOR=0455
   ITEMNEWBIE=i_halberd
   COLOR=0455

[EVENTS e_revenant]
ON=@Death
   UID.<SRC.TAG.Owner>.FINDID.i_revenant_tracker.REMOVE
   UID.<SRC.TAG.Owner>.SYSMESSAGE Your revenant failed
ON=@Kill
   IF (<ARGO.UID>==<SRC.TAG.Victim>)
      UID.<SRC.TAG.Owner>.FINDID.i_revenant_tracker.REMOVE
      UID.<SRC.TAG.Owner>.SYSMESSAGE Your revenant succeeded!
      REMOVE
   ENDIF

//////////////
// Exorcism //
//////////////

[FUNCTION isnearcorpse]
REF12=<UID>
FORITEMS <ARGS>
   IF (<TYPE>==t_corpse)
      IF (<LINK.UID>==<REF12.UID>)
         RETURN 1
      ENDIF
   ENDIF
ENDFOR
RETURN 0


[EOF]
