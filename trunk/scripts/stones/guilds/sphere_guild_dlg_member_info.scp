///////////////////////////////////////////
/// Script feito por Coruja //////////////
/////////////////////////////////////////

[DIALOG d_guild_member_info]
10,40

resizepic 0 0 0242C 350 255
dhtmlgump 20 15 310 26 0 0 <DEF.CENTER><DEF.I>Guild Member Information
gumppictiled 20 40 310 2 02711

dhtmlgump 20 50 150 26 1 0 <DEF.I>Name
dhtmlgump 180 53 150 26 0 0 <NAME>

dhtmlgump 20 80 150 26 1 0 <DEF.I>Rank
dhtmlgump 180 83 150 26 0 0 <DEF.Guild.RankName.<dGUILD.MemberFromUID.<UID>.PRIV>>

dhtmlgump 20 110 150 26 1 0 <DEF.I>Guild Title
dhtmlgump 180 113 150 26 0 0 <GUILD.MemberFromUID.<UID>.GuildTitle>
gumppictiled 20 142 310 2 02711

IF (<DEF0.Guild.EnableVoteForGuildMaster>)
 resizepic 20 150 02486 310 26
 button 25 155 0845 0846 1 0 1
 dhtmlgump 50 153 270 26 0 0 <QVAL (<SRC.GUILD.MemberFromUID.<SRC>.LoyalTo>==<UID>)? Clear:Cast> Vote For This Member
ENDIF

resizepic 20 180 02486 150 26
button 25 185 0845 0846 1 0 2
dhtmlgump 50 183 110 26 0 0 <QVAL (<SRC.TAG0.Guild.ToLeader>)? <DEF.BFONT_RED>>Promote

resizepic 20 210 02486 150 26
button 25 215 0845 0846 1 0 3
dhtmlgump 50 213 110 26 0 0 Demote

resizepic 180 180 02486 150 26
button 185 185 0845 0846 1 0 4
dhtmlgump 210 183 110 26 0 0 Set Guild Title

resizepic 180 210 02486 150 26
button 185 215 0845 0846 1 0 5
dhtmlgump 210 213 110 26 0 0 <QVAL (<SRC.TAG0.Guild.ToKick>)? <DEF.BFONT_RED>>Kick


[DIALOG d_guild_member_info BUTTON]
ON=0
SRC.TAG0.Guild.ToLeader=
SRC.TAG0.Guild.ToKick=

ON=1
IF !(<DEF0.Guild.EnableVoteForGuildMaster>)
 return 1
ELIF (<GUILD.MemberFromUID.<SRC>.LoyalTo>==<UID>) && (<GUILD.MasterUID> != <UID>)
 SRC.SYSMESSAGE @,,1 You have cleared your vote for guild leader.
 UID.<GUILD>.MemberFromUID.<SRC>.LoyalTo=<SRC>
ELIF (<GUILD.MemberFromUID.<SRC>.PRIV> < <DEF0.Guild.Priv.CanVoteForGuildMaster>)
 SRC.SYSMESSAGE @,,1 You don't have permission to vote.
ELIF (<GUILD.MasterUID>==<SRC>)
 SRC.SYSMESSAGE @,,1 You can't vote for the current guild leader.
ELIF (<GUILD.MemberFromUID.<UID>.PRIV>==0)
 SRC.SYSMESSAGE @,,1 You can't vote for an <STRTOLOWER <DEF0.Guild.RankName.0>> guild member.
//ELIF (<Guild_CanBeVotedFor>)
// SRC.SYSMESSAGE @,,1 You can't vote for an inactive guild member.
ELSE
 REF1=<GUILD.MasterUID>
 UID.<GUILD>.MemberFromUID.<SRC>.LoyalTo=<UID>
 UID.<GUILD>.ElectMaster	//Internal function
 SRC.SYSMESSAGE @,,1 You cast your vote for <NAME> for guild leader.
 IF (<GUILD.MasterUID> != <REF1>)
  UID.<GUILD>.Guild_SendMessage Guildmaster changed to: <UID.<GUILD.MasterUID>.NAME>
 ENDIF
ENDIF

ON=2
IF (<GUILD.MemberFromUID.<SRC>.PRIV> < <DEF0.Guild.Priv.CanPromoteDemote>) || (<GUILD.MemberFromUID.<UID>.PRIV> >= <GUILD.MemberFromUID.<SRC>.PRIV>)
 SRC.SYSMESSAGE @,,1 You don't have permission to promote this member.
ELIF (<GUILD.MemberFromUID.<UID>.PRIV>==0)
 UID.<GUILD>.JoinAsMember <UID>
 SRC.SYSMESSAGE @,,1 The guild information for <NAME> has been updated.
ELIF (<GUILD.MemberFromUID.<UID>.PRIV>==1)
 IF (<SRC.TAG0.Guild.ToLeader>)
  UID.<GUILD>.MasterUID=<UID>
  SRC.SYSMESSAGE @,,1 The guild information for <NAME> has been updated.
  SRC.SYSMESSAGE @,,1 The guild information for <SRC.NAME> has been updated.
  SRC.TAG0.Guild.ToLeader=
 ELSE
  SRC.SYSMESSAGE @,,1 Are you sure you wish to make this member the new guild leader?
  SRC.TAG0.Guild.ToLeader=1
  SDIALOG d_guild_member_info
 ENDIF
ENDIF

ON=3
IF (<GUILD.MemberFromUID.<SRC>.PRIV> < <DEF0.Guild.Priv.CanPromoteDemote>) || (<GUILD.MemberFromUID.<UID>.PRIV> >= <GUILD.MemberFromUID.<SRC>.PRIV>)
 SRC.SYSMESSAGE @,,1 You don't have permission to demote this member.
ELIF (<GUILD.MemberFromUID.<UID>.PRIV>==1)
 UID.<SRC.GUILD>.Resign <UID>
 UID.<SRC.GUILD>.ApplyToJoin <UID>
 SRC.SYSMESSAGE @,,1 The guild information for <NAME> has been updated.
ELSE
 SRC.SYSMESSAGE @,,1 You can't demote a <DEF.Guild.RankName.<dGUILD.MemberFromUID.<UID>.PRIV>>.
ENDIF

ON=4
IF (<GUILD.MemberFromUID.<SRC>.PRIV> < <DEF0.Guild.Priv.CanSetMemberTitle>) || ((<GUILD.MemberFromUID.<UID>.PRIV> >= <GUILD.MemberFromUID.<SRC>.PRIV>) && (<SRC> != <UID>))
 SRC.SYSMESSAGE @,,1 You don't have <QVAL (<isempty <GUILD.MemberFromUID.<UID>.GuildTitle>>)? the permission to set that:permission to change this> member's guild title.
ELSE
 SRC.SYSMESSAGE @,,1 Enter the new title for this guild member or 'none' to remove a title:
 SRC.CTAG.Guild.Temp.SetTitle=<UID>
 PROMPTCONSOLE Guild_SetTitle_CallBack
ENDIF

ON=5
IF (<GUILD.MemberFromUID.<SRC>.PRIV> < <DEF0.Guild.Priv.CanKickPlayers>) || (<GUILD.MemberFromUID.<UID>.PRIV> >= <GUILD.MemberFromUID.<SRC>.PRIV>)
 SRC.SYSMESSAGE @,,1 You don't have permission to remove this member.
ELIF (<SRC.TAG0.Guild.ToKick>)
 UID.<GUILD>.Guild_RemoveMember <UID>
 SRC.SYSMESSAGE @,,1 The member has been removed from your guild.
 SRC.TAG0.Guild.ToKick=
ELSE
 SRC.SYSMESSAGE @,,1 Are you sure you wish to kick this member from the guild?
 SRC.TAG0.Guild.ToKick=1
 SDIALOG d_guild_member_info
ENDIF

[EOF]